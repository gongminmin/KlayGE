IF(KLAYGE_PLATFORM_WINDOWS_DESKTOP)
	SET(KLAYGE_LibOVR_PATH "" CACHE STRING "Path of LibOVR")
	IF(KLAYGE_LibOVR_PATH)
		SET(KLAYGE_LIBOVR_FOUND TRUE)
	ENDIF()
ELSEIF(KLAYGE_PLATFORM_WINDOWS_STORE)
	STRING(REPLACE "/permissive-" "" CMAKE_CXX_FLAGS ${CMAKE_CXX_FLAGS})
ENDIF()

SET(LIB_NAME KlayGE_InputEngine_MsgInput)

SET(MSGINPUT_IE_SOURCE_FILES
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MIJoystick.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MIKeyboard.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MIMouse.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MInputEngine.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MInputFactory.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MISensor.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MITouch.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MIOVR.cpp
)

SET(MSGINPUT_IE_HEADER_FILES
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/MsgInput/MInput.hpp
)

SOURCE_GROUP("Source Files" FILES ${MSGINPUT_IE_SOURCE_FILES})
SOURCE_GROUP("Header Files" FILES ${MSGINPUT_IE_HEADER_FILES})

IF(KLAYGE_LIBOVR_FOUND)
	ADD_DEFINITIONS(-DKLAYGE_HAVE_LIBOVR)
ENDIF()
IF(KLAYGE_COMPILER_MSVC)
	SET(CMAKE_SHARED_LINKER_FLAGS_DEBUG "${CMAKE_SHARED_LINKER_FLAGS_DEBUG} /INCREMENTAL:NO")
ENDIF()

INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIR})
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../KFL/include)
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/Core/Include)
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/Plugins/Include)
IF(KLAYGE_PLATFORM_ANDROID)
	INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/android_native_app_glue)
ENDIF()
LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../KFL/lib/${KLAYGE_PLATFORM_NAME})
IF(KLAYGE_PLATFORM_DARWIN OR KLAYGE_PLATFORM_LINUX)
	LINK_DIRECTORIES(${KLAYGE_BIN_DIR})
ELSE()
	LINK_DIRECTORIES(${KLAYGE_OUTPUT_DIR})
ENDIF()
IF(KLAYGE_LIBOVR_FOUND)
	INCLUDE_DIRECTORIES(${KLAYGE_LibOVR_PATH}/Include)
	LINK_DIRECTORIES(${KLAYGE_LibOVR_PATH}/Lib/${KLAYGE_VS_PLATFORM_NAME})
ENDIF()

ADD_LIBRARY(${LIB_NAME} ${KLAYGE_PREFERRED_LIB_TYPE}
	${MSGINPUT_IE_SOURCE_FILES} ${MSGINPUT_IE_HEADER_FILES}
)
ADD_DEPENDENCIES(${LIB_NAME} ${KLAYGE_CORELIB_NAME})

IF(KLAYGE_PLATFORM_WINDOWS_DESKTOP)
	SET(EXTRA_LINKED_LIBRARIES ${EXTRA_LINKED_LIBRARIES}
		locationapi sensorsapi)
ENDIF()

IF(KLAYGE_LIBOVR_FOUND)
	SET(EXTRA_LINKED_LIBRARIES ${EXTRA_LINKED_LIBRARIES}
		winmm
		debug libovrd64 optimized libovr64)
ENDIF()

SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES
	ARCHIVE_OUTPUT_DIRECTORY ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_DEBUG ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELEASE ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELWITHDEBINFO ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_MINSIZEREL ${KLAYGE_OUTPUT_DIR}
	PROJECT_LABEL ${LIB_NAME}
	DEBUG_POSTFIX ${CMAKE_DEBUG_POSTFIX}
	OUTPUT_NAME ${LIB_NAME}${KLAYGE_OUTPUT_SUFFIX}
)

ADD_PRECOMPILED_HEADER(${LIB_NAME} "KlayGE/KlayGE.hpp" "${KLAYGE_PROJECT_DIR}/Core/Include" "${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MInputFactory.cpp")

TARGET_LINK_LIBRARIES(${LIB_NAME}
	${EXTRA_LINKED_LIBRARIES}
	debug KlayGE_Core${KLAYGE_OUTPUT_SUFFIX}_d optimized KlayGE_Core${KLAYGE_OUTPUT_SUFFIX}
	debug KFL${KLAYGE_OUTPUT_SUFFIX}_d optimized KFL${KLAYGE_OUTPUT_SUFFIX}
)

IF(KLAYGE_PREFERRED_LIB_TYPE STREQUAL "SHARED")
	ADD_POST_BUILD(${LIB_NAME} "Input")
 
	INSTALL(TARGETS ${LIB_NAME}
		RUNTIME DESTINATION ${KLAYGE_BIN_DIR}/Input
		LIBRARY DESTINATION ${KLAYGE_BIN_DIR}/Input
		ARCHIVE DESTINATION ${KLAYGE_OUTPUT_DIR}
	)
ENDIF()

SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES FOLDER "Engine/Plugins/Input")

ADD_DEPENDENCIES(AllInEngine ${LIB_NAME})

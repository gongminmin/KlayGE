SET(LIB_NAME KlayGE_RenderEngine_OpenGL)

SET(OPENGL_RE_SOURCE_FILES
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLFrameBuffer.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLGraphicsBuffer.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLMapping.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLQuery.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLRenderEngine.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLRenderFactory.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLRenderLayout.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLRenderStateObject.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLRenderView.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLRenderWindow.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLShaderObject.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLTexture.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLTexture1D.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLTexture2D.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLTexture3D.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLTextureCube.cpp
)

SET(OPENGL_RE_HEADER_FILES
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLFrameBuffer.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLGraphicsBuffer.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLMapping.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLQuery.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLRenderEngine.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLRenderFactory.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLRenderFactoryInternal.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLRenderLayout.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLRenderStateObject.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLRenderView.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLRenderWindow.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLShaderObject.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGL/OGLTexture.hpp
)

SOURCE_GROUP("Source Files" FILES ${OPENGL_RE_SOURCE_FILES})
SOURCE_GROUP("Header Files" FILES ${OPENGL_RE_HEADER_FILES})

SET(USE_DXBC2GLSL 1)

ADD_DEFINITIONS(-DKLAYGE_BUILD_DLL -DKLAYGE_OGL_RE_SOURCE)
ADD_DEFINITIONS(-DUSE_DXBC2GLSL=${USE_DXBC2GLSL})

STRING(FIND "${CMAKE_GENERATOR_TOOLSET}" "_xp" IS_XP_TOOLSET)
SET(USE_DX_SDK FALSE)
IF(MSVC)
	IF(MSVC_VERSION LESS 1699)
		SET(USE_DX_SDK TRUE)
	ELSEIF(NOT (${IS_XP_TOOLSET} LESS 0))
		SET(USE_DX_SDK TRUE)
	ENDIF()
ELSE()
	SET(USE_DX_SDK TRUE)
ENDIF()

INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIR})
IF(USE_DX_SDK)
	INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/DXSDK/Include)
ENDIF()
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../KFL/include)
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/Core/Include)
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/Plugins/Include)
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../glloader/include)
IF(USE_DXBC2GLSL)
	INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../DXBC2GLSL/Include)
ELSE()
	INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/Cg/include)
ENDIF()
LINK_DIRECTORIES(${Boost_LIBRARY_DIR})
LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../KFL/lib/${KLAYGE_PLATFORM_NAME})
LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/lib/${KLAYGE_PLATFORM_NAME})
LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../glloader/lib/${KLAYGE_PLATFORM_NAME})
IF(USE_DXBC2GLSL)
	LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../DXBC2GLSL/lib/${KLAYGE_PLATFORM_NAME})
ELSE()
	IF(KLAYGE_ARCH_NAME MATCHES "x64")
		LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/Cg/lib.x64)
	ELSE()
		LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/Cg/lib)
	ENDIF()
ENDIF()

ADD_LIBRARY(${LIB_NAME} SHARED
	${OPENGL_RE_SOURCE_FILES} ${OPENGL_RE_HEADER_FILES}
)
ADD_DEPENDENCIES(${LIB_NAME} ${KLAYGE_CORELIB_NAME})

IF(USE_DXBC2GLSL)
	SET(EXTRA_LINKED_LIBRARIES
		debug DXBC2GLSLLib${KLAYGE_OUTPUT_SUFFIX}_d optimized DXBC2GLSLLib${KLAYGE_OUTPUT_SUFFIX})
ELSE()
	SET(EXTRA_LINKED_LIBRARIES cg)
ENDIF()

IF(NOT MSVC)
	IF(KLAYGE_PLATFORM_NAME MATCHES "linux")
		SET(EXTRA_LINKED_LIBRARIES ${EXTRA_LINKED_LIBRARIES} MesaGL)
	ENDIF()

	SET(EXTRA_LINKED_LIBRARIES ${EXTRA_LINKED_LIBRARIES}
		debug KlayGE_Core${KLAYGE_OUTPUT_SUFFIX}_d optimized KlayGE_Core${KLAYGE_OUTPUT_SUFFIX}
		debug KFL${KLAYGE_OUTPUT_SUFFIX}_d optimized KFL${KLAYGE_OUTPUT_SUFFIX}
		${Boost_CHRONO_LIBRARY} ${Boost_THREAD_LIBRARY} ${Boost_SYSTEM_LIBRARY})
ENDIF()

SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES
	ARCHIVE_OUTPUT_DIRECTORY ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_DEBUG ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELEASE ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELWITHDEBINFO ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_MINSIZEREL ${KLAYGE_OUTPUT_DIR}
	PROJECT_LABEL ${LIB_NAME}
	DEBUG_POSTFIX ${CMAKE_DEBUG_POSTFIX}
	OUTPUT_NAME ${LIB_NAME}${KLAYGE_OUTPUT_SUFFIX}
)

ADD_PRECOMPILED_HEADER(${LIB_NAME} "KlayGE/KlayGE.hpp" "${KLAYGE_PROJECT_DIR}/Core/Include" "${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGL/OGLRenderFactory.cpp")

TARGET_LINK_LIBRARIES(${LIB_NAME}
	debug glloader${KLAYGE_OUTPUT_SUFFIX}_d optimized glloader${KLAYGE_OUTPUT_SUFFIX}
	${EXTRA_LINKED_LIBRARIES}
)


ADD_POST_BUILD(${LIB_NAME} "Render")

 
INSTALL(TARGETS ${LIB_NAME}
	RUNTIME DESTINATION ${KLAYGE_BIN_DIR}/Render
	LIBRARY DESTINATION ${KLAYGE_BIN_DIR}/Render
	ARCHIVE DESTINATION ${KLAYGE_OUTPUT_DIR}
)

SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES FOLDER "Rendering System")

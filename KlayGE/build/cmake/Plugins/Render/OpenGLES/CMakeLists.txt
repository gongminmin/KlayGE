FIND_PACKAGE(GLES)

IF(KLAYGE_GLES_FOUND)

SET(LIB_NAME KlayGE_RenderEngine_OpenGLES)

SET(OPENGLES_RE_SOURCE_FILES
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESFrameBuffer.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESGraphicsBuffer.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESMapping.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESQuery.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESRenderEngine.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESRenderFactory.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESRenderLayout.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESRenderStateObject.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESRenderView.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESRenderWindow.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESShaderObject.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESTexture.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESTexture1D.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESTexture2D.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESTexture3D.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESTextureCube.cpp
)

SET(OPENGLES_RE_HEADER_FILES
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESFrameBuffer.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESGraphicsBuffer.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESMapping.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESQuery.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESRenderEngine.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESRenderFactory.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESRenderFactoryInternal.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESRenderLayout.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESRenderStateObject.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESRenderView.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESRenderWindow.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESShaderObject.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/OpenGLES/OGLESTexture.hpp
)

SOURCE_GROUP("Source Files" FILES ${OPENGLES_RE_SOURCE_FILES})
SOURCE_GROUP("Header Files" FILES ${OPENGLES_RE_HEADER_FILES})

SET(USE_DXBC2GLSL 1)

ADD_DEFINITIONS(-DKLAYGE_BUILD_DLL -DKLAYGE_OGLES_RE_SOURCE -DGLLOADER_GLES_SUPPORT)
ADD_DEFINITIONS(-DUSE_DXBC2GLSL=${USE_DXBC2GLSL})
IF(USE_DXBC2GLSL)
	IF(KLAYGE_PLATFORM_DARWIN OR KLAYGE_PLATFORM_LINUX)
		FIND_PATH(WINE_PATH "winegcc" #searching for 'wine' may result in include path
			PATHS /usr/local/bin opt/local/sbin /usr/bin /bin /usr/sbin /sbin /usr/local/bin)

		IF(NOT WINE_PATH)
			MESSAGE(FATAL_ERROR "Cound NOT find wine")
		ENDIF()
		ADD_DEFINITIONS(-DWINE_PATH="\\"${WINE_PATH}/\\"")
	ENDIF()
ENDIF()

STRING(FIND "${CMAKE_GENERATOR_TOOLSET}" "_xp" IS_XP_TOOLSET)
SET(USE_DX_SDK FALSE)
IF(MSVC)
	IF(MSVC_VERSION LESS 1699)
		SET(USE_DX_SDK TRUE)
	ELSEIF(NOT (${IS_XP_TOOLSET} LESS 0))
		SET(USE_DX_SDK TRUE)
	ENDIF()
ELSE()
	SET(USE_DX_SDK TRUE)
ENDIF()

INCLUDE_DIRECTORIES(${KLAYGE_GLES_INCLUDE_DIR})
INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIR})
IF(USE_DX_SDK)
	INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/DXSDK/Include)
ENDIF()
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../KFL/include)
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/Core/Include)
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/Plugins/Include)
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../glloader/include)
IF(APPLE AND NOT IOS)
	INCLUDE_DIRECTORIES(/opt/X11/include)
ENDIF()
IF(USE_DXBC2GLSL)
	INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../DXBC2GLSL/Include)
ELSE()
	INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/Cg/include)
ENDIF()
IF(KLAYGE_PLATFORM_ANDROID)
	INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/android_native_app_glue)
ENDIF()
LINK_DIRECTORIES(${Boost_LIBRARY_DIR})
LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../KFL/lib/${KLAYGE_PLATFORM_NAME})
LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/lib/${KLAYGE_PLATFORM_NAME})
LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../glloader/lib/${KLAYGE_PLATFORM_NAME})
IF(USE_DXBC2GLSL)
	LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../DXBC2GLSL/lib/${KLAYGE_PLATFORM_NAME})
ELSE()
	LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/Cg/lib/${KLAYGE_PLATFORM_NAME})
ENDIF()

ADD_LIBRARY(${LIB_NAME} ${KLAYGE_PREFERRED_LIB_TYPE}
	${OPENGLES_RE_SOURCE_FILES} ${OPENGLES_RE_HEADER_FILES}
)
ADD_DEPENDENCIES(${LIB_NAME} ${KLAYGE_CORELIB_NAME})

IF(USE_DXBC2GLSL)
	SET(EXTRA_LINKED_LIBRARIES
		debug DXBC2GLSLLib${KLAYGE_OUTPUT_SUFFIX}_d optimized DXBC2GLSLLib${KLAYGE_OUTPUT_SUFFIX})
ELSE()
	SET(EXTRA_LINKED_LIBRARIES Cg)
ENDIF()

IF(NOT MSVC)
	SET(EXTRA_LINKED_LIBRARIES ${EXTRA_LINKED_LIBRARIES}
		debug KlayGE_Core${KLAYGE_OUTPUT_SUFFIX}_d optimized KlayGE_Core${KLAYGE_OUTPUT_SUFFIX}
		debug KFL${KLAYGE_OUTPUT_SUFFIX}_d optimized KFL${KLAYGE_OUTPUT_SUFFIX}
		${Boost_CHRONO_LIBRARY} ${Boost_THREAD_LIBRARY} ${Boost_SYSTEM_LIBRARY})
ENDIF()

SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES
	ARCHIVE_OUTPUT_DIRECTORY ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_DEBUG ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELEASE ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELWITHDEBINFO ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_MINSIZEREL ${KLAYGE_OUTPUT_DIR}
	PROJECT_LABEL ${LIB_NAME}
	DEBUG_POSTFIX ${CMAKE_DEBUG_POSTFIX}
	OUTPUT_NAME ${LIB_NAME}${KLAYGE_OUTPUT_SUFFIX}
)

ADD_PRECOMPILED_HEADER(${LIB_NAME} "KlayGE/KlayGE.hpp" "${KLAYGE_PROJECT_DIR}/Core/Include" "${KLAYGE_PROJECT_DIR}/Plugins/Src/Render/OpenGLES/OGLESRenderFactory.cpp")

TARGET_LINK_LIBRARIES(${LIB_NAME}
	debug glloader_es${KLAYGE_OUTPUT_SUFFIX}_d optimized glloader_es${KLAYGE_OUTPUT_SUFFIX}
	${EXTRA_LINKED_LIBRARIES}
)

IF(KLAYGE_PREFERRED_LIB_TYPE STREQUAL "SHARED")
	ADD_POST_BUILD(${LIB_NAME} "Render")
 
	INSTALL(TARGETS ${LIB_NAME}
		RUNTIME DESTINATION ${KLAYGE_BIN_DIR}/Render
		LIBRARY DESTINATION ${KLAYGE_BIN_DIR}/Render
		ARCHIVE DESTINATION ${KLAYGE_OUTPUT_DIR}
	)
ENDIF()

SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES FOLDER "Rendering System")

ENDIF()

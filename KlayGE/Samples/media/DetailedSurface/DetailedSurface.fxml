<?xml version='1.0'?>

<effect>
	<include name="util.fxml"/>
	<include name="Quaternion.fxml"/>
	<include name="Lighting.fxml"/>
	<include name="JudaTexture.fxml"/>
	<include name="Tessellation.fxml"/>
	<include name="Mesh.fxml"/>

	<cbuffer name="per_frame">
		<parameter type="float4x4" name="mvp" semantic="WORLDVIEWPROJECTION"/>
		<parameter type="float4x4" name="world"/>
		<parameter type="float4x4" name="model_view"/>
		<parameter type="float3" name="eye_pos"/>
		<parameter type="float3" name="forward_vec"/>
		<parameter type="float3" name="light_pos"/>
		<parameter type="float3" name="light_color"/>
		<parameter type="float3" name="light_falloff"/>
		<parameter type="int2" name="frame_size"/>
	</cbuffer>

	<parameter type="float" name="height_scale"/>

	<parameter type="int4" name="diffuse_tex_bb"/>
	<parameter type="int4" name="normal_tex_bb"/>
	<parameter type="int4" name="height_tex_bb"/>
	<parameter type="int4" name="occlusion_tex_bb"/>
	<parameter type="int2" name="tex_size"/>

	<parameter type="bool" name="use_occlusion_map"/>
	<parameter type="bool" name="use_na_length"/>
	<parameter type="texture2D" name="na_length_tex"/>
	<parameter type="sampler" name="linear_sampler">
		<state name="filtering" value="min_mag_mip_linear"/>
		<state name="address_u" value="wrap"/>
		<state name="address_v" value="wrap"/>
	</parameter>

	<parameter type="int" name="lod_threshold" value="3"/>
	<parameter type="int" name="min_samples" value="8"/>
	<parameter type="int" name="max_samples" value="50"/>
	<parameter type="float" name="shadow_softening" value="0.58"/>

	<shader>
		<![CDATA[
void CalcTBN(float4 pos,
					float2 texcoord0,
					float4 T,
					out float4 oL,
					out float4 oH,
					out float3 oV,
					out float4 oPos)
{
	oPos = mul(pos, mvp);
	
	float3x3 obj_to_tangent;
	obj_to_tangent[0] = transform_quat(float3(1, 0, 0), T);
	obj_to_tangent[1] = transform_quat(float3(0, 1, 0), T) * sign(T.w);
	obj_to_tangent[2] = transform_quat(float3(0, 0, 1), T);

	float3 light_vec = light_pos - pos.xyz;
	float3 view_vec = eye_pos - pos.xyz;
	float3 half_vec = normalize(light_vec) + normalize(view_vec);

	oL.xyz = mul(obj_to_tangent, light_vec);
	oH.xyz = mul(obj_to_tangent, half_vec);
	oV = mul(obj_to_tangent, view_vec);
	
	oL.w = texcoord0.x;
	oH.w = texcoord0.y;
}

void CalcTBNVS(float4 pos				: POSITION,
					float2 texcoord0	: TEXCOORD0,
					float4 tangent_quat : TANGENT,
					out float4 oL			: TEXCOORD0,	// in tangent space
					out float4 oH			: TEXCOORD1,	// in tangent space
					out float3 oV			: TEXCOORD2,
					out float4 oPos			: SV_Position)
{
	pos = float4(pos.xyz * pos_extent + pos_center, 1);
	texcoord0 = texcoord0 * tc_extent + tc_center;
	tangent_quat = tangent_quat * 2 - 1;

	CalcTBN(pos, texcoord0, tangent_quat,
		oL, oH, oV, oPos);
}

float4 Shading(float3 L, float3 H, float2 uv, float3 normal, float shadow, bool nl, bool om)
{
	float atten = AttenuationTerm(0, -L.xyz, light_falloff);

	float3 diffuse = judatex2d_wrap(diffuse_tex_bb, uv).rgb;

	float3 light_vec = normalize(L);
	float3 halfway = normalize(H);

	float shininess = 128;
	if (nl)
	{
		float len = get_x_channel(na_length_tex.Sample(linear_sampler, uv));
		float ft = len / lerp(shininess, 1, len);
		shininess *= ft;
	}

	float occlusion = 1;
	if (om)
	{
		occlusion = get_x_channel(judatex2d_wrap(occlusion_tex_bb, uv));
	}
	
	float3 shading = 0.1f * diffuse + atten * light_color * CalcBRDFShading(diffuse, 0.2f, shininess, light_vec, halfway, normal);
	return float4(shading * occlusion * shadow, 1);
}

float4 NonePS(float4 L		: TEXCOORD0,
					float4 H		: TEXCOORD1) : SV_Target
{
	float2 uv = float2(L.w, H.w);
	return Shading(L.xyz, H.xyz, uv, float3(0, 0, 1), 1, 0, 0);
}

float4 BumpMappingPS(float4 L		: TEXCOORD0,
					float4 H		: TEXCOORD1) : SV_Target
{
	float2 uv = float2(L.w, H.w);
	return Shading(L.xyz, H.xyz, uv, restore_normal(judatex2d_wrap(normal_tex_bb, uv).rg * 2 - 1), 1, use_na_length, use_occlusion_map);
}

float4 ParallaxMappingPS(float4 L		: TEXCOORD0,
					float4 H			: TEXCOORD1,
					float3 V			: TEXCOORD2) : SV_Target
{
	float2 texCoord0 = float2(L.w, H.w);
	
	float3 view = normalize(V);
	
	float height = (judatex2d_wrap(height_tex_bb, texCoord0).r - 1) * height_scale;
	float2 texUV = texCoord0 + (view.xy * height);

	return Shading(L.xyz, H.xyz, texUV, restore_normal(judatex2d_wrap(normal_tex_bb, texUV).rg * 2 - 1), 1,
		use_na_length, use_occlusion_map);
}

float4 ParallaxOcclusionMappingPS(float4 L		: TEXCOORD0,
					float4 H					: TEXCOORD1,
					float3 V					: TEXCOORD2) : SV_Target
{
	float4 vParallaxOffsetTS = float4(normalize(V).xy, normalize(L.xyz).xy) * height_scale;

	float3 view = normalize(V);
	float2 texCoord = float2(L.w, H.w);

	float level = mipmap_level(texCoord, tex_size);

	float2 texUV = texCoord;
	float fOcclusionShadow = 1;
	if (level <= lod_threshold)
	{
		int nNumSteps = (int)lerp(max_samples, min_samples, dot(view, float3(0, 0, 1)));

		float fStepSize = 1.0f / nNumSteps;
		float fPrevHeight = 1;
		float fNextHeight = 0;

		int nStepIndex = 0;

		float2 vTexOffsetPerStep = fStepSize * vParallaxOffsetTS.xy;
		float2 vTexCurrentOffset = texCoord;
		float fCurrentBound = 1;

		float2 pt1 = 0;
		float2 pt2 = 0;

		while (nStepIndex < nNumSteps)
		{
			vTexCurrentOffset -= vTexOffsetPerStep;
			float fCurrHeight = judatex2d_level_wrap(height_tex_bb, vTexCurrentOffset, level).r;
			fCurrentBound -= fStepSize;

			if (fCurrHeight > fCurrentBound)
			{
				pt1 = float2(fCurrentBound, fCurrHeight);
				pt2 = float2(fCurrentBound + fStepSize, fPrevHeight);

				nStepIndex = nNumSteps;
			}
			else
			{
				++ nStepIndex;
			}
			fPrevHeight = fCurrHeight;
		}

		float fDelta2 = pt2.x - pt2.y;
		float fDelta1 = pt1.x - pt1.y;

		float fDenominator = fDelta2 - fDelta1;
		float fParallaxAmount;
		if (0 == fDenominator)
		{
			fParallaxAmount = 0;
		}
		else
		{
			fParallaxAmount = (pt1.x * fDelta2 - pt2.x * fDelta1) / fDenominator;
		}

		float2 texSampleBase = texCoord - vParallaxOffsetTS.xy * (1 - fParallaxAmount);
		if (level > lod_threshold - 1)
		{
			// Lerp the texture coordinate from parallax occlusion mapped coordinate to bump mapping
			// smoothly based on the current mip level
			texUV = lerp(texSampleBase, texCoord, frac(level));
		}
		else
		{
			texUV = texSampleBase;
		}

		// shadow
		{
			float2 vLightRayTS = vParallaxOffsetTS.zw;

			// Compute the soft blurry shadows taking into account self-occlusion for 
			// features of the height field

			float sh0 = judatex2d_level_wrap(height_tex_bb, texSampleBase, level).r;
			float4 shA987 = float4(judatex2d_level_wrap(height_tex_bb, texSampleBase + vLightRayTS * 0.88f, level).r - 0.88f,
				judatex2d_level_wrap(height_tex_bb, texSampleBase + vLightRayTS * 0.77f, level).r - 0.77f,
				judatex2d_level_wrap(height_tex_bb, texSampleBase + vLightRayTS * 0.66f, level).r - 0.66f,
				judatex2d_level_wrap(height_tex_bb, texSampleBase + vLightRayTS * 0.55f, level).r - 0.55f);
			float3 sh654 = float3(judatex2d_level_wrap(height_tex_bb, texSampleBase + vLightRayTS * 0.44f, level).r - 0.44f,
				judatex2d_level_wrap(height_tex_bb, texSampleBase + vLightRayTS * 0.33f, level).r - 0.33f,
				judatex2d_level_wrap(height_tex_bb, texSampleBase + vLightRayTS * 0.22f, level).r - 0.22f);
			shA987 = (shA987 - sh0) * shadow_softening * float4(1, 2, 4, 6);
			sh654 = (sh654 - sh0) * shadow_softening * float3(8, 10, 12);

			// Compute the actual shadow strength
			fOcclusionShadow = 1 - max(max(max(max(max(max(shA987.x, shA987.y), shA987.z), shA987.w), sh654.x), sh654.y), sh654.z);

			// The previous computation overbrightens the image, let's adjust for that
			fOcclusionShadow = saturate(fOcclusionShadow * 0.6f + 0.4f);
		}
	}

	return Shading(L.xyz, H.xyz, texUV, restore_normal(judatex2d_level_wrap(normal_tex_bb, texUV, level).rg * 2 - 1), fOcclusionShadow,
		use_na_length, use_occlusion_map);
}
		]]>
	</shader>

	<technique name="None">
		<pass name="p0">
			<state name="vertex_shader" value="CalcTBNVS()"/>
			<state name="pixel_shader" value="NonePS()"/>
		</pass>
	</technique>

	<technique name="Bump">
		<pass name="p0">
			<state name="vertex_shader" value="CalcTBNVS()"/>
			<state name="pixel_shader" value="BumpMappingPS()"/>
		</pass>
	</technique>
	
	<technique name="Parallax">
		<pass name="p0">
			<state name="vertex_shader" value="CalcTBNVS()"/>
			<state name="pixel_shader" value="ParallaxMappingPS()"/>
		</pass>
	</technique>

	<technique name="ParallaxOcclusion">
		<pass name="p0">
			<state name="vertex_shader" value="CalcTBNVS()"/>
			<state name="pixel_shader" value="ParallaxOcclusionMappingPS()"/>
		</pass>
	</technique>


	<technique name="NoneWireframe" inherit="None">
		<pass name="p0">
			<state name="polygon_mode" value="line"/>
		</pass>
	</technique>

	<technique name="BumpWireframe" inherit="Bump">
		<pass name="p0">
			<state name="polygon_mode" value="line"/>
		</pass>
	</technique>

	<technique name="ParallaxWireframe" inherit="Parallax">
		<pass name="p0">
			<state name="polygon_mode" value="line"/>
		</pass>
	</technique>

	<technique name="ParallaxOcclusionWireframe" inherit="ParallaxOcclusion">
		<pass name="p0">
			<state name="polygon_mode" value="line"/>
		</pass>
	</technique>

	<shader version="5">
		<![CDATA[
struct VS_CONTROL_POINT_OUTPUT
{
	float3 Pos : POSITION;
	float2 Texcoord0 : TEXCOORD0;
	float4 TangentQuat : TEXCOORD1;
};

VS_CONTROL_POINT_OUTPUT TessellationVS(float4 pos	: POSITION,
					float2 texcoord0	: TEXCOORD0,
					float4 tangent_quat			: TANGENT)
{
	pos = float4(pos.xyz * pos_extent + pos_center, 1);
	texcoord0 = texcoord0 * tc_extent + tc_center;
	tangent_quat = tangent_quat * 2 - 1;

	VS_CONTROL_POINT_OUTPUT output;
	
	output.Pos = pos.xyz;
	output.Texcoord0 = texcoord0;
	output.TangentQuat = tangent_quat;

	return output;
}

struct HS_CONSTANT_DATA_OUTPUT
{
	// Tess factor for the FF HW block
	float edges[3]	: SV_TessFactor;
	float inside	: SV_InsideTessFactor;
};

float2 ScreenCoord(float3 pos)
{
	float4 p = mul(float4(pos, 1), mvp);
	p /= p.w;
	p.y *= KLAYGE_FLIPPING;
	return (p.xy * 0.5f + 0.5f) * frame_size;
}

float EdgeFactor(float2 p0_ss, float2 p1_ss, float3 p0_ws, float3 p1_ws)
{
	float np = distance(p0_ss, p1_ss);
	float dist = length(mul(float4((p0_ws + p1_ws) / 2, 1), model_view).xyz);
	return np / dist;
}

HS_CONSTANT_DATA_OUTPUT TessellationConstantsHS(InputPatch<VS_CONTROL_POINT_OUTPUT, 3> ip)
{
	float4 tess_factors = float4(10, 10, 1, 20);

	HS_CONSTANT_DATA_OUTPUT output;

	const float BACK_FACE_THRESHOLD = 0.4f;
		
	float3 normal0 = transform_quat(float3(0, 0, 1), ip[0].TangentQuat);
	float3 normal1 = transform_quat(float3(0, 0, 1), ip[1].TangentQuat);
	float3 normal2 = transform_quat(float3(0, 0, 1), ip[2].TangentQuat);
		
	float3 edge0_normal = (normal2 + normal0) * 0.5f;
	float3 edge1_normal = (normal0 + normal1) * 0.5f;
	float3 edge2_normal = (normal1 + normal2) * 0.5f;

	float3 orientation;
	orientation.x = dot(edge0_normal, forward_vec);
	orientation.y = dot(edge1_normal, forward_vec);
	orientation.z = dot(edge2_normal, forward_vec);
		
	if (all(orientation > BACK_FACE_THRESHOLD))
	{
		output.inside = 0;
		output.edges[0] = 0;
		output.edges[1] = 0;
		output.edges[2] = 0;
	}
	else
	{
		float2 p0 = ScreenCoord(ip[0].Pos);
		float2 p1 = ScreenCoord(ip[1].Pos);
		float2 p2 = ScreenCoord(ip[2].Pos);

		float3 edges_factors = float3(EdgeFactor(p2, p0, ip[2].Pos, ip[0].Pos),
				EdgeFactor(p0, p1, ip[0].Pos, ip[1].Pos),
				EdgeFactor(p1, p2, ip[1].Pos, ip[2].Pos))
			* tess_factors.x * 0.3f;

		output.edges[0] = clamp(edges_factors.x, tess_factors.z, tess_factors.w);
		output.edges[1] = clamp(edges_factors.y, tess_factors.z, tess_factors.w);
		output.edges[2] = clamp(edges_factors.z, tess_factors.z, tess_factors.w);
		output.inside = clamp((edges_factors.x + edges_factors.y + edges_factors.z) / 3,
			tess_factors.z, tess_factors.w);
	}

	return output;
}

[domain("tri")]
[partitioning("fractional_odd")]
[outputtopology("triangle_cw")]
[outputcontrolpoints(3)]
[patchconstantfunc("TessellationConstantsHS")]
VS_CONTROL_POINT_OUTPUT TessellationHS(InputPatch<VS_CONTROL_POINT_OUTPUT, 3> p, 
										uint i : SV_OutputControlPointID)
{
	return p[i];
}

void PostTessellation(float3 bc_coords, float3 pos, float2 texcoord0, float4 tangent_quat,
						out float4 oL, out float4 oH, out float3 oV,
						out float4 oPos)
{
	float3x3 obj_to_tangent;
	obj_to_tangent[0] = transform_quat(float3(1, 0, 0), tangent_quat);
	obj_to_tangent[1] = transform_quat(float3(0, 1, 0), tangent_quat) * sign(tangent_quat.w);
	obj_to_tangent[2] = transform_quat(float3(0, 0, 1), tangent_quat);

	float height = (judatex2d_level_wrap(height_tex_bb, texcoord0, 0).r - 1) * height_scale * 0.05f;
	pos += height * obj_to_tangent[2];

	oPos = mul(float4(pos, 1), mvp);

	float3 light_vec = light_pos - pos.xyz;
	float3 view_vec = eye_pos - pos.xyz;
	float3 half_vec = normalize(light_vec) + normalize(view_vec);

	oL.xyz = mul(obj_to_tangent, light_vec);
	oH.xyz = mul(obj_to_tangent, half_vec);
	oV = mul(obj_to_tangent, view_vec);

	oL.w = texcoord0.x;
	oH.w = texcoord0.y;
}

[domain("tri")]
void FlatTessellationDS(HS_CONSTANT_DATA_OUTPUT HSConstantData, 
							float3 bc_coords : SV_DomainLocation,
							const OutputPatch<VS_CONTROL_POINT_OUTPUT, 3> patch,

							out float4 oL : TEXCOORD0,
							out float4 oH : TEXCOORD1,
							out float3 oV : TEXCOORD2,
							out float4 oPos : SV_Position)
{
	float3 pos;
	float2 texcoord0;
	float4 tangent_quat;
	InterpolateFlatAttrs(bc_coords,
				patch[0].Pos, patch[1].Pos, patch[2].Pos,
				patch[0].Texcoord0, patch[1].Texcoord0, patch[2].Texcoord0,
				patch[0].TangentQuat, patch[1].TangentQuat, patch[2].TangentQuat,

				pos, texcoord0,
				tangent_quat);
				
	PostTessellation(bc_coords, pos, texcoord0, tangent_quat,
				oL, oH, oV, oPos);
}

[domain("tri")]
void SmoothTessellationDS(HS_CONSTANT_DATA_OUTPUT HSConstantData, 
							float3 bc_coords : SV_DomainLocation,
							const OutputPatch<VS_CONTROL_POINT_OUTPUT, 3> patch,

							out float4 oL : TEXCOORD0,
							out float4 oH : TEXCOORD1,
							out float3 oV : TEXCOORD2,
							out float4 oPos : SV_Position)
{
	float3 pos;
	float2 texcoord0;
	float4 tangent_quat;
	InterpolatePhongAttrs(bc_coords,
				patch[0].Pos, patch[1].Pos, patch[2].Pos,
				patch[0].Texcoord0, patch[1].Texcoord0, patch[2].Texcoord0,
				patch[0].TangentQuat, patch[1].TangentQuat, patch[2].TangentQuat,

				pos, texcoord0,
				tangent_quat);

	PostTessellation(bc_coords, pos, texcoord0, tangent_quat,
				oL, oH, oV, oPos);
}
		]]>
	</shader>

	<technique name="FlatTessellation">
		<pass name="p0">
			<state name="vertex_shader" value="TessellationVS()"/>
			<state name="hull_shader" value="TessellationHS()"/>
			<state name="domain_shader" value="FlatTessellationDS()"/>
			<state name="pixel_shader" value="BumpMappingPS()"/>
		</pass>
	</technique>

	<technique name="FlatTessellationWireframe" inherit="FlatTessellation">
		<pass name="p0">
			<state name="polygon_mode" value="line"/>
		</pass>
	</technique>

	<technique name="SmoothTessellation">
		<pass name="p0">
			<state name="vertex_shader" value="TessellationVS()"/>
			<state name="hull_shader" value="TessellationHS()"/>
			<state name="domain_shader" value="SmoothTessellationDS()"/>
			<state name="pixel_shader" value="BumpMappingPS()"/>
		</pass>
	</technique>

	<technique name="SmoothTessellationWireframe" inherit="SmoothTessellation">
		<pass name="p0">
			<state name="polygon_mode" value="line"/>
		</pass>
	</technique>
</effect>

<?xml version='1.0'?>

<effect>
	<include name="InfTerrain.fxml"/>
	<include name="Quaternion.fxml"/>

	<cbuffer name="per_frame">
		<parameter type="float4x4" name="billboard_mat"/>
	</cbuffer>

	<shader>
		<![CDATA[
static const float PI = 3.1415926f;

void FoliageGBufferVS(float4 pos : POSITION,
			float2 texcoord : TEXCOORD0,
			float4 tangent_quat : TANGENT,
#if SKINNING_ON
			float4 blend_weights : BLENDWEIGHT,
#if KLAYGE_OPENGL || KLAYGE_OPENGLES
			float4 blend_indices_f : BLENDINDICES,
#else
			uint4 blend_indices : BLENDINDICES,
#endif
#endif
			float4 instance_pos_scale : TEXCOORD1,
			float2 instance_rotation : TEXCOORD2,
			out float4 oTexCoord_2xy : TEXCOORD0,
			out float4 oTsToView0_2z : TEXCOORD1,
			out float4 oTsToView1_Depth : TEXCOORD2,
#ifdef NOPERSPECTIVE_SUPPORT
			out noperspective float2 oScreenTc : TEXCOORD3,
#else
			out float3 oScreenTc : TEXCOORD3,
#endif
			out float4 oPos : SV_Position)
{
#if SKINNING_ON
#if KLAYGE_OPENGL || KLAYGE_OPENGLES
	int4 blend_indices = (int4)blend_indices_f;
#endif
#endif

	float4 rotation_y_quat = float4(0, instance_rotation.x, 0, instance_rotation.y);

	pos = float4(pos.xyz * pos_extent + pos_center, 1);
	texcoord = texcoord * tc_extent + tc_center;
	tangent_quat = tangent_quat * 2 - 1;
	
	pos.xyz *= instance_pos_scale.w;
	pos.xyz = transform_quat(pos.xyz, rotation_y_quat);
	pos.xyz += instance_pos_scale.xyz;

	tangent_quat = mul_quat(tangent_quat, rotation_y_quat);

	float3 result_pos;
	float4 result_tangent_quat;
	CalcMesh(pos, texcoord, tangent_quat,
#if SKINNING_ON
				blend_weights, blend_indices,
#endif
				oTexCoord_2xy.xy, result_pos,
				result_tangent_quat);

	oPos = mul(float4(result_pos, 1), mvp);

	float3x3 obj_to_ts;
	obj_to_ts[0] = transform_quat(float3(1, 0, 0), result_tangent_quat);
	obj_to_ts[1] = transform_quat(float3(0, 1, 0), result_tangent_quat) * sign(result_tangent_quat.w);
	obj_to_ts[2] = transform_quat(float3(0, 0, 1), result_tangent_quat);
	float3x3 ts_to_view = mul(obj_to_ts, (float3x3)model_view);
	oTsToView0_2z.xyz = ts_to_view[0];
	oTsToView1_Depth.xyz = ts_to_view[1];
	oTexCoord_2xy.zw = ts_to_view[2].xy;
	oTsToView0_2z.w = ts_to_view[2].z;

	oTsToView1_Depth.w = oPos.w;

	oScreenTc.xy = oPos.xy / oPos.w * 0.5f;
	oScreenTc.y *= KLAYGE_FLIPPING;
	oScreenTc.xy += 0.5f;

#ifndef NOPERSPECTIVE_SUPPORT
	oScreenTc.z = oPos.w;
	oScreenTc.xy *= oScreenTc.z;
#endif
}
		]]>
	</shader>

	<technique name="FoliageGBufferMRT" inherit="GBufferMRTTech">
		<pass name="p0">
			<state name="vertex_shader" value="FoliageGBufferVS()"/>
		</pass>
	</technique>

	<technique name="FoliageGBufferAlphaTestMRT" inherit="GBufferAlphaTestMRTTech">
		<pass name="p0">
			<state name="vertex_shader" value="FoliageGBufferVS()"/>
		</pass>
	</technique>

	<technique name="GenFoliageNoTessTerrainShadowMapTech" inherit="GenNoTessTerrainShadowMapTech">
		<pass name="p0">
			<state name="vertex_shader" value="FoliageGBufferVS()"/>
		</pass>
	</technique>

	<technique name="GenFoliageNoTessTerrainCascadedShadowMapTech" inherit="GenNoTessTerrainCascadedShadowMapTech">
		<pass name="p0">
			<state name="vertex_shader" value="FoliageGBufferVS()"/>
		</pass>
	</technique>

	<shader>
		<![CDATA[
void FoliageImpostorGBufferVS(float2 pos2 : POSITION,
			float4 instance_pos_scale : TEXCOORD1,
			float2 instance_rotation : TEXCOORD2,
			out float4 oTexCoord_2xy : TEXCOORD0,
			out float4 oTsToView0_2z : TEXCOORD1,
			out float4 oTsToView1_Depth : TEXCOORD2,
#ifdef NOPERSPECTIVE_SUPPORT
			out noperspective float2 oScreenTc : TEXCOORD3,
#else
			out float3 oScreenTc : TEXCOORD3,
#endif
			out float4 oPos : SV_Position)
{
	float4 pos = float4(pos2 * pos_extent.xy + pos_center.xy, 0, 1);
	float2 texcoord = float2(pos2.x, -pos2.y) * tc_extent + instance_rotation;

	pos.xyz *= instance_pos_scale.w;
	pos.xyz = mul(pos.xyz, (float3x3)billboard_mat);
	pos.xyz += instance_pos_scale.xyz;

	oPos = mul(pos, mvp);
	oTexCoord_2xy.xy = texcoord;

	float3x3 obj_to_ts;
	obj_to_ts[0] = mul(float3(1, 0, 0), (float3x3)billboard_mat);
	obj_to_ts[1] = mul(float3(0, 1, 0), (float3x3)billboard_mat);
	obj_to_ts[2] = mul(float3(0, 0, 1), (float3x3)billboard_mat);
	float3x3 ts_to_view = mul(obj_to_ts, (float3x3)model_view);
	oTsToView0_2z.xyz = ts_to_view[0];
	oTsToView1_Depth.xyz = ts_to_view[1];
	oTexCoord_2xy.zw = ts_to_view[2].xy;
	oTsToView0_2z.w = ts_to_view[2].z;

	oTsToView1_Depth.w = oPos.w;

	oScreenTc.xy = oPos.xy / oPos.w * 0.5f;
	oScreenTc.y *= KLAYGE_FLIPPING;
	oScreenTc.xy += 0.5f;

#ifndef NOPERSPECTIVE_SUPPORT
	oScreenTc.z = oPos.w;
	oScreenTc.xy *= oScreenTc.z;
#endif
}

void FoliageImpostorGBufferAlphaTestMRTPS(float4 texcoord_2xy : TEXCOORD0, float4 ts_to_view0_2z : TEXCOORD1, float3 ts_to_view1 : TEXCOORD2,
					out float4 mrt_0 : SV_Target0, out float4 mrt_1 : SV_Target1)
{
	mrt_0 = normal_tex.Sample(bilinear_sampler, texcoord_2xy.xy);
	mrt_1 = albedo_tex.Sample(bilinear_sampler, texcoord_2xy.xy);
	clip(dot(mrt_1, mrt_1) - 1.0f / 255);
}
		]]>
	</shader>

	<technique name="FoliageImpostorGBufferAlphaTestMRT" inherit="GBufferAlphaTestMRTTech">
		<pass name="p0">
			<state name="vertex_shader" value="FoliageImpostorGBufferVS()"/>
			<state name="pixel_shader" value="FoliageImpostorGBufferAlphaTestMRTPS()"/>
		</pass>
	</technique>

	<parameter type="uint4" name="num_tile_plants"/>
	<parameter type="uint2" name="tile_addr_offset_width"/>
	<parameter type="float" name="foliage_tile_size"/>
	<parameter type="float" name="half_num_tiles_edge"/>
	<parameter type="float4" name="plant_spacing"/>
	<parameter type="float2" name="impostor_dist"/>
	<parameter type="float2" name="foliage_world_uv_repeats"/>
	<parameter type="float3" name="foliage_texture_world_offset"/>
	<parameter type="float" name="foliage_vertical_scale"/>
	<parameter type="float4x4" name="model_mat"/>
	<parameter type="float2" name="eye_pos_xz"/>
	<parameter type="float3" name="plant_aabb_min"/>
	<parameter type="float3" name="plant_aabb_max"/>
	<parameter type="float4" name="view_frustum_planes" array_size="6"/>
	<parameter type="uint" name="probability_channel"/>
	<parameter type="float4" name="center_tc_angles_step"/>

	<parameter type="texture2D" name="height_map_tex"/>
	<parameter type="texture2D" name="probability_tex">
		<annotation type="string" name="SasResourceAddress" value="probability.dds"/>
	</parameter>
	<parameter type="texture2D" name="noise_tex">
		<annotation type="string" name="SasResourceAddress" value="white_noise_tex.dds"/>
	</parameter>

	<shader>
		<![CDATA[
void TransformAABB(inout float3 aabb_min, inout float3 aabb_max, float3 scale, float4 rot, float3 trans)
{
	float3 corners[] = 
	{
		float3(aabb_min.x, aabb_min.y, aabb_min.z),
		float3(aabb_max.x, aabb_min.y, aabb_min.z),
		float3(aabb_min.x, aabb_max.y, aabb_min.z),
		float3(aabb_max.x, aabb_max.y, aabb_min.z),
		float3(aabb_min.x, aabb_min.y, aabb_max.z),
		float3(aabb_max.x, aabb_min.y, aabb_max.z),
		float3(aabb_min.x, aabb_max.y, aabb_max.z),
		float3(aabb_max.x, aabb_max.y, aabb_max.z)
	};
			
	aabb_min = aabb_max = transform_quat(corners[0] * scale, rot) + trans;
	for (int j = 1; j < 8; ++ j)
	{
		float3 vec = transform_quat(corners[j] * scale, rot) + trans;
		aabb_min = min(aabb_min, vec);
		aabb_max = max(aabb_max, vec);
	}
}

int IntersectAABBFrustum(float3 aabb_min, float3 aabb_max, float4 view_frustum_planes[6])
{
	bool intersect = false;
	for (int i = 0; i < 6; ++ i)
	{
		float4 plane = view_frustum_planes[i];

		// v1 is diagonally opposed to v0
		float3 v0 = float3((plane.x < 0) ? aabb_min.x : aabb_max.x, (plane.y < 0) ? aabb_min.y : aabb_max.y, (plane.z < 0) ? aabb_min.z : aabb_max.z);
		float3 v1 = float3((plane.x < 0) ? aabb_max.x : aabb_min.x, (plane.y < 0) ? aabb_max.y : aabb_min.y, (plane.z < 0) ? aabb_max.z : aabb_min.z);

		if (dot(plane, float4(v0, 1)) < 0)
		{
			return 1;
		}
		if (dot(plane, float4(v1, 1)) < 0)
		{
			intersect = true;
		}
	}

	return intersect ? 2 : 0;
}

void FoliageDistribution(uint vid, bool is_impostor,
					out float4 out_pos_scale,
					out float2 out_rotation)
{
	out_pos_scale = -1;
	out_rotation = -1;
	
	uint tile_index = vid / num_tile_plants.x;
	uint plant_index = vid - tile_index * num_tile_plants.x;
	
	uint iv = plant_index / num_tile_plants.z;
	uint iu = plant_index - iv * num_tile_plants.z;
	float2 tile_uv = float2(iu, iv) / (num_tile_plants.yz - 1);

	uint2 tile_xy;
	tile_xy.y = tile_index / tile_addr_offset_width.y;
	tile_xy.x = tile_index - tile_xy.y * tile_addr_offset_width.y;
	tile_xy += tile_addr_offset_width.x;

	float3 displaced_pos;
	displaced_pos.xz = (tile_xy - half_num_tiles_edge + tile_uv) * foliage_tile_size
		+ (float2(noise_tex.SampleLevel(linear_wrap_sampler, tile_uv * 13 / 256, 0).x,
				noise_tex.SampleLevel(linear_wrap_sampler, tile_uv * 23 / 256, 0).x) - 0.5f) * plant_spacing.xy;
	displaced_pos.y = 0;

	float2 global_uv = displaced_pos.xz + 2 * foliage_texture_world_offset.xz;

	float2 probobility_uv = global_uv + float2(noise_tex.SampleLevel(linear_wrap_sampler, global_uv * 7, 0).x,
		noise_tex.SampleLevel(linear_wrap_sampler, global_uv * 5, 0).x) - 0.5f;
	float probability = probability_tex.SampleLevel(linear_wrap_sampler, probobility_uv, 0)[probability_channel];
	bool foliage_planted = (probability >= noise_tex.SampleLevel(linear_wrap_sampler, global_uv * 3, 0).x);

	float3 world_displaced_pos = mul(float4(displaced_pos, 1), model_mat).xyz;
	float2 dist = world_displaced_pos.xz - eye_pos_xz;
	float dist_sq = dot(dist, dist);

	bool in_dist = false;
	if (((impostor_dist.y >= 0) && (dist_sq < impostor_dist.y)) || (impostor_dist.y < 0))
	{
		if (is_impostor)
		{
			if (dist_sq >= impostor_dist.x)
			{
				in_dist = true;
			}
		}
		else
		{
			if (dist_sq < impostor_dist.x)
			{
				in_dist = true;
			}
		}
	}
	
	if (foliage_planted && in_dist)
	{
		float2 height_uv = displaced_pos.xz * foliage_world_uv_repeats.y * 0.5f + 0.5f;
		float height = height_map_tex.SampleLevel(linear_wrap_sampler, height_uv, 0).x * foliage_vertical_scale;

		displaced_pos = world_displaced_pos + height * model_mat[1].xyz;

		float scale = noise_tex.SampleLevel(linear_wrap_sampler, global_uv * 17, 0).x * 1.7f + 0.3f;
		float angle = noise_tex.SampleLevel(linear_wrap_sampler, global_uv * 39, 0).x * PI;
		float4 rot_quat = axis_angle_to_quat(float3(0, 1, 0), angle);

		float3 aabb_min = plant_aabb_min;
		float3 aabb_max = plant_aabb_max;
		TransformAABB(aabb_min, aabb_max, scale, rot_quat, displaced_pos);
		if (IntersectAABBFrustum(aabb_min, aabb_max, view_frustum_planes) != 1)
		{
			out_pos_scale = float4(displaced_pos, scale);

			if (is_impostor)
			{
				out_rotation = center_tc_angles_step.xy;
				out_rotation.x += floor(angle * center_tc_angles_step.z + 0.5f) * center_tc_angles_step.w;
			}
			else
			{
				out_rotation = rot_quat.yw;
				if (rot_quat.w < 0)
				{
					out_rotation = -out_rotation;
				}
			}
		}
	}
}

void FoliageDistributionVS(uint vid : SV_VertexID,
					out float4 out_pos_scale : TEXCOORD1,
					out float2 out_rotation : TEXCOORD2)
{
	FoliageDistribution(vid, false, out_pos_scale, out_rotation);
}

void FoliageImpostorDistributionVS(uint vid : SV_VertexID,
					out float4 out_pos_scale : TEXCOORD1,
					out float2 out_rotation : TEXCOORD2)
{
	FoliageDistribution(vid, true, out_pos_scale, out_rotation);
}

struct FoliageDistGSOut
{
	float4 pos_scale : TEXCOORD1;
	float2 rotation : TEXCOORD2;
};

[maxvertexcount(1)]
void FoliageDistributionGS(point float4 in_pos_scale[1] : TEXCOORD1, point float2 in_rotation[1] : TEXCOORD2,
		inout PointStream<FoliageDistGSOut> out_stream)
{
	if (in_pos_scale[0].w > 0)
	{
		FoliageDistGSOut gs_out;
		gs_out.pos_scale = in_pos_scale[0];
		gs_out.rotation = in_rotation[0];
		out_stream.Append(gs_out);
	}
}
		]]>
	</shader>

	<technique name="FoliageDistribution">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="FoliageDistributionVS()"/>
			<state name="geometry_shader" value="FoliageDistributionGS()">
				<stream_output>
					<entry usage="TEXCOORD" usage_index="1" component="xyzw" slot="0"/>
					<entry usage="TEXCOORD" usage_index="2" component="xy" slot="0"/>
				</stream_output>
			</state>
		</pass>
	</technique>

	<technique name="FoliageImpostorDistribution" inherit="FoliageDistribution">
		<pass name="p0">
			<state name="vertex_shader" value="FoliageImpostorDistributionVS()"/>
		</pass>
	</technique>
</effect>

<?xml version='1.0'?>

<effect>
	<include name="Lighting.fxml"/>
	<include name="util.fxml"/>
	<include name="Quaternion.fxml"/>

	<parameter type="float4x4" name="worldviewproj" semantic="WORLDVIEWPROJECTION"/>
	<parameter type="float3" name="eye_pos"/>
	<parameter type="float3" name="light_pos"/>
	<parameter type="float3" name="light_color"/>
	<parameter type="float3" name="light_falloff"/>
	<parameter type="float3" name="extinction_coefficient"/>
	<parameter type="float" name="material_thickness"/>
	<parameter type="float" name="sigma_t"/>
	<parameter type="float2" name="far_plane"/>
	
	<parameter type="float3" name="pos_center"/>
	<parameter type="float3" name="pos_extent"/>
	<parameter type="float2" name="tc_center"/>
	<parameter type="float2" name="tc_extent"/>

	<parameter type="sampler" name="linear_sampler">
		<state name="filtering" value="min_mag_mip_linear"/>
		<state name="address_u" value="wrap"/>
		<state name="address_v" value="wrap"/>
	</parameter>

	<parameter type="float4" name="ambient_clr"/>
	<parameter type="float4" name="diffuse_clr"/>
	<parameter type="float4" name="specular_clr"/>
	<parameter type="float2" name="shininess_clr"/>
	
	<parameter type="texture2D" name="diffuse_tex"/>
	<parameter type="texture2D" name="bump_tex"/>
	<parameter type="texture2D" name="specular_tex"/>
	<parameter type="texture2D" name="shininess_tex"/>

	<shader>
		<![CDATA[
void BackFaceDepthVS(float2 tex0 : TEXCOORD0,
				float4 pos : POSITION,
				out float oDepth : TEXCOORD0,
				out float4 oPos : SV_Position)
{
	pos = float4(pos.xyz * pos_extent + pos_center, 1);
	oPos = mul(pos, worldviewproj);
	oDepth = oPos.w;
}

float4 BackFaceDepthPS(float depth : TEXCOORD0) : SV_Target
{
#if KLAYGE_PACK_TO_RGBA
	return EncodeFloatRGBA(clamp(depth * far_plane.y, 0, 0.999f));
#else
	return depth;
#endif
}
		]]>
	</shader>

	<technique name="BackFaceDepthTechWODepthTexture">
		<pass name="p0">
			<state name="cull_mode" value="front"/>
			<state name="depth_func" value="greater"/>

			<state name="vertex_shader" value="BackFaceDepthVS()"/>
			<state name="pixel_shader" value="BackFaceDepthPS()"/>
		</pass>
	</technique>
	<technique name="BackFaceDepthTech" inherit="BackFaceDepthTechWODepthTexture">
		<pass name="p0">
			<state name="color_write_mask" value="0"/>
		</pass>
	</technique>

	<parameter type="texture2D" name="back_face_depth_tex"/>
	<parameter type="float2" name="near_q"/>

	<parameter type="sampler" name="point_sampler">
		<state name="filtering" value="min_mag_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
	</parameter>

	<shader>
		<![CDATA[
void SubSurfaceVS(float2 tex0 : TEXCOORD0,
				float4 pos : POSITION,
				float4 tangent_quat	: TANGENT,	// in object space
				
				out float2 oTex0 : TEXCOORD0,
				out float3 oL	: TEXCOORD1,
				out float3 oV	: TEXCOORD2,
				out float3 oH	: TEXCOORD3,
				out float3 oN	: TEXCOORD4,
				out float3 oTcW : TEXCOORD5,
				out float4 oPos : SV_Position)
{
	pos = float4(pos.xyz * pos_extent + pos_center, 1);
	tex0 = tex0 * tc_extent + tc_center;
	tangent_quat = tangent_quat * 2 - 1;
	
	oTex0 = tex0;
	
	oV = eye_pos - pos.xyz;
	oL = light_pos - pos.xyz;
	oH = normalize(oL) + normalize(oV);
	oN = transform_quat(float3(0, 0, 1), tangent_quat);

	oPos = mul(pos, worldviewproj);
	
	oTcW.xy = oPos.xy / oPos.w;
	oTcW.y *= KLAYGE_FLIPPING;
	oTcW.xy = oTcW.xy * 0.5f + 0.5f;	
	oTcW.z = oPos.w;
	oTcW.xy *= oTcW.z;
}

float4 SubSurfacePS(float2 uv : TEXCOORD0,
				float3 L	: TEXCOORD1,
				float3 V	: TEXCOORD2,
				float3 H	: TEXCOORD3,
				float3 N	: TEXCOORD4,
				float3 tc_w : TEXCOORD5) : SV_Target
{
	float attenuation = attenuation_term(0, -L.xyz, light_falloff);

	L = normalize(L);
	V = normalize(V);
	H = normalize(H);
	N = normalize(N + 0.11f * bump_tex.Sample(linear_sampler, uv).rgb);
	
	float3 diffuse, specular;
	float shininess;
	if (diffuse_clr.a > 0.5f)
	{
		diffuse = diffuse_tex.Sample(linear_sampler, uv).rgb;
	}
	else
	{
		diffuse = diffuse_clr.rgb;
	}
	if (specular_clr.a > 0.5f)
	{
		specular = specular_tex.Sample(linear_sampler, uv).rgb;
	}
	else
	{
		specular = specular_clr.rgb;
	}	
	if (shininess_clr.y > 0.5f)
	{
		shininess = shininess_tex.Sample(linear_sampler, uv).r;
	}
	else
	{
		shininess = shininess_clr.r;
	}

	float2 tex_coord = tc_w.xy / tc_w.z;
#if KLAYGE_PACK_TO_RGBA
	float back_face_depth = DecodeFloatRGBA(back_face_depth_tex.Sample(point_sampler, tex_coord)) * far_plane.x;
#else
	float back_face_depth = back_face_depth_tex.Sample(point_sampler, tex_coord).r;
#endif
#if !NO_DEPTH_TEXTURE
	back_face_depth = non_linear_depth_to_linear(back_face_depth, near_q.x, near_q.y);
#endif
	float thickness = clamp(back_face_depth - tc_w.z, 0, 100.0f);

	float indirect_light = material_thickness * (min(0, dot(N, L)) + min(0, dot(V, L)));
	indirect_light *= exp(thickness * sigma_t);

	float3 clr = (ambient_clr.rgb * diffuse + calc_brdf(diffuse, specular, shininess, L, H, N)
		+ indirect_light * extinction_coefficient) * attenuation;

	return float4(clr.rgb * light_color, 1);
}
		]]>
	</shader>

	<technique name="SubSurfaceTech">
		<pass name="p0">
			<state name="cull_mode" value="back"/>

			<state name="vertex_shader" value="SubSurfaceVS()"/>
			<state name="pixel_shader" value="SubSurfacePS()"/>
		</pass>
	</technique>
	<technique name="SubSurfaceTechWODepthTexture" inherit="SubSurfaceTech">
		<macro name="NO_DEPTH_TEXTURE" value="1"/>
	</technique>
</effect>

<?xml version='1.0'?>

<effect>
	<include name="util.fxml"/>

	<parameter type="sampler" name="point_sampler">
		<state name="filtering" value="min_mag_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
	</parameter>
	
	<parameter type="sampler" name="bilinear_sampler">
		<state name="filtering" value="min_mag_linear_mip_point"/>
		<state name="address_u" value="wrap"/>
		<state name="address_v" value="wrap"/>
		<state name="address_w" value="wrap"/>
	</parameter>

	<parameter type="texture2D" name="particle_pos_tex"/>
	<parameter type="texture3D" name="noise_vol_tex"/>

	<parameter type="float" name="point_radius"/>
	<parameter type="float4" name="init_pos_life"/>

	<cbuffer name="per_frame">
		<parameter type="float4x4" name="View"/>
		<parameter type="float4x4" name="Proj"/>
		<parameter type="float4x4" name="mvp"/>
		<parameter type="float4x4" name="inv_view"/>
		<parameter type="float4x4" name="inv_proj"/>

		<parameter type="float" name="far_plane"/>
	</cbuffer>

	<parameter type="texture2D" name="particle_tex"/>
	<parameter type="texture2D" name="scene_tex"/>

	<shader>
		<![CDATA[
struct PS_IN
{
	float4 pos : SV_Position;
	float4 clr : COLOR0;
	float4 PosSS : TEXCOORD0;
	float4 CenterView : TEXCOORD1;
	float3 view_dir : TEXCOORD2;
	float2 tex : TEXCOORD3;
};
		]]>
	</shader>
	
	<shader>
		<![CDATA[
void ParticlesVS(float2 tex_pos		: POSITION,
					float2 tex			: TEXCOORD0,
					out PS_IN vs_out)
{
	float4 pos = particle_pos_tex.SampleLevel(point_sampler, tex_pos, 0);

	if (pos.w <= 0)
	{
		vs_out.pos = float4(-1, -1, 0, 0);
		vs_out.clr = 0;
		vs_out.PosSS = 0;
		vs_out.CenterView = 0;
		vs_out.view_dir = 0;
		vs_out.tex = 0;
	}
	else
	{
		vs_out.CenterView = mul(float4(pos.xyz, 1), View);
		float4 view_pos = vs_out.CenterView;
		view_pos.xy += tex * point_radius;
		vs_out.pos = mul(view_pos, Proj);
			  
		vs_out.clr = float4(1, 1, 1, pos.w);
		vs_out.PosSS = vs_out.pos;
		
		vs_out.view_dir = view_pos.xyz;
		
		vs_out.tex = tex * 0.5f + 0.5f;
	}
}

float4 ParticlesPS(PS_IN ps_in) : SV_Target
{
	ps_in.CenterView /= ps_in.CenterView.w;
	float3 intersect = ps_in.view_dir;
	
	float life = ps_in.clr.a;
	float4 clr = particle_tex.Sample(bilinear_sampler, ps_in.tex);
	
	float2 tex_coord = ps_in.PosSS.xy / ps_in.PosSS.w;
	tex_coord.y *= KLAYGE_FLIPPING;
	tex_coord = tex_coord / 2 + 0.5f;

	float depth = scene_tex.SampleLevel(point_sampler, tex_coord, 0).a * far_plane;
	float optical_depth = depth - intersect.z;
		
	if (optical_depth < 0)
	{
		clr = 0;
	}
	else
	{
		optical_depth = min(optical_depth, clr.w);
		float4 intersect_ws = mul(float4(intersect, 1), inv_view);
		float3 normal = clr.xyz * 2 - 1;
#if KLAYGE_MAX_TEX_DEPTH > 1
		float4 n = noise_vol_tex.SampleLevel(bilinear_sampler, intersect_ws.xyz, 0);
		normal = lerp(normal, n.xyz * 2 - 1, 0.2f);
#endif
		clr = float4(normal.xyz, optical_depth * clr.w * saturate(life / init_pos_life.w));
	}
	
	return clr;
}
		]]>
	</shader>
	
	<technique name="Particles">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>
			
			<state name="blend_enable" value="true"/>
			<state name="blend_op" value="add"/>
			<state name="src_blend" value="one"/>
			<state name="dest_blend" value="one"/>
			<state name="blend_op_alpha" value="add"/>
			<state name="src_blend_alpha" value="one"/>
			<state name="dest_blend_alpha" value="one"/>
			
			<state name="vertex_shader" value="ParticlesVS()"/>
			<state name="pixel_shader" value="ParticlesPS()"/>
		</pass>
	</technique>

	<parameter type="texture2D" name="particle_vel_tex"/>

	<cbuffer name="per_frame2">
		<parameter type="float4x4" name="ps_model_mat"/>
		<parameter type="float" name="accumulate_time"/>
		<parameter type="float" name="elapse_time"/>
	</cbuffer>

	<parameter type="texture2D" name="particle_init_vel_tex"/>
	<parameter type="texture2D" name="particle_birth_time_tex"/>
	<parameter type="buffer" elem_type="float4" name="particle_init_vel_buff"/>
	<parameter type="buffer" elem_type="float" name="particle_birth_time_buff"/>

	<parameter type="texture2D" name="height_map_tex"/>
	<parameter type="texture2D" name="normal_map_tex"/>
	<parameter type="sampler" name="normal_map_sampler">
		<state name="filtering" value="min_mag_linear_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
	</parameter>

	<shader>
		<![CDATA[
void UpdateVS(float4 pos			: POSITION,
					float2 tex			: TEXCOORD0,
					out float2 oTex		: TEXCOORD0,
					out float4 oPos		: SV_Position)
{
	oPos = pos;
	oTex = float2(tex.x, 1 - tex.y);
}

void UpdatePS(float2 tex : TEXCOORD0,
				out float4 out_pos : SV_Target0,
				out float4 out_vel : SV_Target1)
{
	float4 cur_pos = particle_pos_tex.Sample(point_sampler, tex);
	if (cur_pos.w > 0)
	{
		float3 cur_vel = particle_vel_tex.SampleLevel(point_sampler, tex, 0).xyz;

		cur_vel += float3(0, -0.1f, 0) * elapse_time;
		cur_pos.xyz += cur_vel * elapse_time;
		cur_pos.w -= elapse_time;
		
		float2 tex_pos = cur_pos.xz / 4 + 0.5;
		tex_pos.y = 1 - tex_pos.y;
		float height = height_map_tex.SampleLevel(point_sampler, tex_pos, 0).r;
		if (cur_pos.y < height)
		{
			cur_vel = reflect(cur_vel, decompress_normal(normal_map_tex.SampleLevel(normal_map_sampler, tex_pos, 0)).xzy) * 0.99f;
		}
		
		out_pos = cur_pos;
		out_vel = float4(cur_vel, 1);
	}
	else
	{
		float t = accumulate_time - particle_birth_time_tex.SampleLevel(point_sampler, tex, 0).r;
		if ((t > 0) && (t < elapse_time))
		{
			float4 pos = mul(float4(init_pos_life.xyz, 1), ps_model_mat);
			pos /= pos.w;

			float3 vel = particle_init_vel_tex.SampleLevel(point_sampler, tex + cur_pos.xz, 0).xyz;
			vel = mul(vel, (float3x3)ps_model_mat);

			out_pos = float4(pos.xyz, init_pos_life.w);
			out_vel = float4(vel, 1);
		}
		else
		{
			out_pos = float4(0, 0, 0, -1);
			out_vel = 0;
		}
	}
}

float4 UpdatePosPS(float2 tex : TEXCOORD0) : SV_Target
{
	float4 out_pos;
	float4 cur_pos = particle_pos_tex.Sample(point_sampler, tex);
	if (cur_pos.w > 0)
	{
		float3 cur_vel = particle_vel_tex.SampleLevel(point_sampler, tex, 0).xyz;

		cur_vel += float3(0, -0.1f, 0) * elapse_time;
		cur_pos.xyz += cur_vel * elapse_time;
		cur_pos.w -= elapse_time;
		
		out_pos = cur_pos;
	}
	else
	{
		float t = accumulate_time - particle_birth_time_tex.SampleLevel(point_sampler, tex, 0).r;
		if ((t > 0) && (t < elapse_time))
		{
			float4 pos = mul(float4(init_pos_life.xyz, 1), ps_model_mat);
			pos /= pos.w;

			out_pos = float4(pos.xyz, init_pos_life.w);
		}
		else
		{
			out_pos = float4(0, 0, 0, -1);
		}
	}
	
	return out_pos;
}

float4 UpdateVelPS(float2 tex : TEXCOORD0) : SV_Target
{
	float4 out_vel;
	float4 cur_pos = particle_pos_tex.Sample(point_sampler, tex);
	if (cur_pos.w > 0)
	{
		float3 cur_vel = particle_vel_tex.SampleLevel(point_sampler, tex, 0).xyz;

		cur_vel += float3(0, -0.1f, 0) * elapse_time;
		cur_pos.xyz += cur_vel * elapse_time;
		cur_pos.w -= elapse_time;
		
		float2 tex_pos = cur_pos.xz / 4 + 0.5;
		tex_pos.y = 1 - tex_pos.y;
		float height = height_map_tex.SampleLevel(point_sampler, tex_pos, 0).r;
		if (cur_pos.y < height)
		{
			cur_vel = reflect(cur_vel, decompress_normal(normal_map_tex.SampleLevel(normal_map_sampler, tex_pos, 0)).xzy) * 0.99f;
		}
		
		out_vel = float4(cur_vel, 1);
	}
	else
	{
		float t = accumulate_time - particle_birth_time_tex.SampleLevel(point_sampler, tex, 0).r;
		if ((t > 0) && (t < elapse_time))
		{
			float3 vel = particle_init_vel_tex.SampleLevel(point_sampler, tex + cur_pos.xz, 0).xyz;
			vel = mul(vel, (float3x3)ps_model_mat);

			out_vel = float4(vel, 1);
		}
		else
		{
			out_vel = 0;
		}
	}
	
	return out_vel;
}
		]]>
	</shader>

	<technique name="Update">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="UpdateVS()"/>
			<state name="pixel_shader" value="UpdatePS()"/>
		</pass>
	</technique>

	<technique name="UpdatePos" inherit="Update">
		<pass name="p0">
			<state name="pixel_shader" value="UpdatePosPS()"/>
		</pass>
	</technique>

	<technique name="UpdateVel" inherit="Update">
		<pass name="p0">
			<state name="pixel_shader" value="UpdateVelPS()"/>
		</pass>
	</technique>


	<shader type="vertex_shader">
		<![CDATA[
void ParticlesVS2(float2 tex_pos : POSITION,
					out float4 CenterView_Life : SV_Position)
{
	float4 pos = particle_pos_tex.SampleLevel(point_sampler, tex_pos, 0);

	CenterView_Life = float4(mul(float4(pos.xyz, 1), View).xyz, pos.w);
}

void ParticlesVS3(float4 pos : POSITION,
					out float4 CenterView_Life : SV_Position)
{
	CenterView_Life = float4(mul(float4(pos.xyz, 1), View).xyz, pos.w);
}
		]]>
	</shader>

	<shader type="geometry_shader" version="4">
		<![CDATA[
[maxvertexcount(4)]
void ParticlesGS(point float4 input[1] : SV_Position, inout TriangleStream<PS_IN> out_stream)
{
	float4 CenterView_Life = input[0];

	if (CenterView_Life.w > 0)
	{
		PS_IN gs_out;

		for (int i = 0; i < 4; ++ i)
		{
			float2 tex = float2((i & 1) ? 1 : -1, (i & 2) ? -1 : 1);

			float4 view_pos = float4(CenterView_Life.xyz, 1);
			gs_out.CenterView = view_pos;
			view_pos.xy += tex * point_radius;
			gs_out.pos = mul(view_pos, Proj);
			gs_out.clr = float4(1, 1, 1, CenterView_Life.w);
			gs_out.PosSS = gs_out.pos;
			gs_out.view_dir = view_pos.xyz;
			gs_out.tex = tex * 0.5f + 0.5f;

			out_stream.Append(gs_out);
		}
	}
}
		]]>
	</shader>

	<technique name="ParticlesWithGS">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="blend_enable" value="true"/>
			<state name="blend_op" value="add"/>
			<state name="src_blend" value="one"/>
			<state name="dest_blend" value="one"/>
			<state name="blend_op_alpha" value="add"/>
			<state name="src_blend_alpha" value="one"/>
			<state name="dest_blend_alpha" value="one"/>

			<state name="vertex_shader" value="ParticlesVS2()"/>
			<state name="geometry_shader" value="ParticlesGS()"/>
			<state name="pixel_shader" value="ParticlesPS()"/>
		</pass>
	</technique>

	<technique name="ParticlesWithGSSO">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="blend_enable" value="true"/>
			<state name="blend_op" value="add"/>
			<state name="src_blend" value="one"/>
			<state name="dest_blend" value="one"/>
			<state name="blend_op_alpha" value="add"/>
			<state name="src_blend_alpha" value="one"/>
			<state name="dest_blend_alpha" value="one"/>

			<state name="vertex_shader" value="ParticlesVS3()"/>
			<state name="geometry_shader" value="ParticlesGS()"/>
			<state name="pixel_shader" value="ParticlesPS()"/>
		</pass>
	</technique>

	<parameter type="buffer" elem_type="float4" name="particle_pos_buff"/>
	<parameter type="buffer" elem_type="float4" name="particle_vel_buff"/>

	<shader type="vertex_shader" version="4">
		<![CDATA[
void UpdateSOVS(uint vid			: SV_VertexID,
					out float4 out_pos	: SV_Position,
					out float4 out_vel	: TEXCOORD0)
{
	float4 cur_pos = particle_pos_buff.Load(vid);
	if (cur_pos.w > 0)
	{
		float3 cur_vel = particle_vel_buff.Load(vid).xyz;
		cur_vel += float3(0, -0.1f, 0) * elapse_time;
		cur_pos.xyz += cur_vel * elapse_time;
		cur_pos.w -= elapse_time;

		float2 tex_pos = cur_pos.xz / 4 + 0.5;
		tex_pos.y = 1 - tex_pos.y;
		float height = height_map_tex.SampleLevel(point_sampler, tex_pos, 0).r;
		if (cur_pos.y < height)
		{
			cur_vel = reflect(cur_vel, decompress_normal(normal_map_tex.SampleLevel(normal_map_sampler, tex_pos, 0)).xzy) * 0.99f;
		}

		out_pos = cur_pos;
		out_vel = float4(cur_vel, 1);
	}
	else
	{
		float t = accumulate_time - particle_birth_time_buff.Load(vid).r;
		if ((t > 0) && (t < elapse_time))
		{
			float4 pos = mul(float4(init_pos_life.xyz, 1), ps_model_mat);
			pos /= pos.w;

			float3 vel = particle_init_vel_buff.Load((vid + uint((cur_pos.z * 256 + cur_pos.x) * 256)) % 65536).xyz;
			vel = mul(vel, (float3x3)ps_model_mat);

			out_pos = float4(pos.xyz, init_pos_life.w);
			out_vel = float4(vel, 1);
		}
		else
		{
			out_pos = float4(0, 0, 0, -1);
			out_vel = 0;
		}
	}
}
		]]>
	</shader>

	<technique name="UpdateSO">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="UpdateSOVS()">
				<stream_output>
					<entry usage="SV_Position" component="xyzw" slot="0"/>
					<entry usage="TEXCOORD" usage_index="0" component="xyzw" slot="1"/>
				</stream_output>
			</state>
		</pass>
	</technique>

	<parameter type="rw_structured_buffer" elem_type="float4" name="particle_pos_rw_stru_buff"/>
	<parameter type="rw_structured_buffer" elem_type="float4" name="particle_vel_rw_stru_buff"/>

	<parameter type="rw_buffer" elem_type="float4" name="particle_pos_rw_buff"/>
	<parameter type="rw_buffer" elem_type="float4" name="particle_vel_rw_buff"/>

	<shader type="compute_shader" version="5">
		<![CDATA[
[numthreads(256, 1, 1)]
void UpdateCS(uint3 dtid : SV_DispatchThreadID)
{
	float4 out_pos;
	float4 out_vel;

	uint id = dtid.x;

#if USE_TYPED_UAV
#if KLAYGE_TYPED_UAV_SUPPORT
	float4 cur_pos = particle_pos_rw_buff[id];
#else
	float4 cur_pos = 0;
#endif
#else
	float4 cur_pos = particle_pos_rw_stru_buff[id];
#endif
	if (cur_pos.w > 0)
	{
#if USE_TYPED_UAV
#if KLAYGE_TYPED_UAV_SUPPORT
		float3 cur_vel = particle_vel_rw_buff[id].xyz;
#else
		float3 cur_vel = 0;
#endif
#else
		float3 cur_vel = particle_vel_rw_stru_buff[id].xyz;
#endif
		cur_vel += float3(0, -0.1f, 0) * elapse_time;
		cur_pos.xyz += cur_vel * elapse_time;
		cur_pos.w -= elapse_time;

		float2 tex_pos = cur_pos.xz / 4 + 0.5;
		tex_pos.y = 1 - tex_pos.y;
		float height = height_map_tex.SampleLevel(point_sampler, tex_pos, 0).r;
		if (cur_pos.y < height)
		{
			cur_vel = reflect(cur_vel, decompress_normal(normal_map_tex.SampleLevel(normal_map_sampler, tex_pos, 0)).xzy) * 0.99f;
		}

		out_pos = cur_pos;
		out_vel = float4(cur_vel, 1);
	}
	else
	{
		float t = accumulate_time - particle_birth_time_buff[id].r;
		if ((t > 0) && (t < elapse_time))
		{
			float4 pos = mul(float4(init_pos_life.xyz, 1), ps_model_mat);
			pos /= pos.w;

			float3 vel = particle_init_vel_buff[(id + uint((cur_pos.z * 256 + cur_pos.x) * 256)) & 0xFFFF].xyz;
			vel = mul(vel, (float3x3)ps_model_mat);

			out_pos = float4(pos.xyz, init_pos_life.w);
			out_vel = float4(vel, 1);
		}
		else
		{
			out_pos = float4(0, 0, 0, -1);
			out_vel = 0;
		}
	}

#if USE_TYPED_UAV
#if KLAYGE_TYPED_UAV_SUPPORT
	particle_pos_rw_buff[id] = out_pos;
	particle_vel_rw_buff[id] = out_vel;
#endif
#else
	particle_pos_rw_stru_buff[id] = out_pos;
	particle_vel_rw_stru_buff[id] = out_vel;
#endif
}
		]]>
	</shader>

	<technique name="UpdateCS">
		<macro name="USE_TYPED_UAV" value="0"/>
		<pass name="p0">
			<state name="compute_shader" value="UpdateCS()"/>
		</pass>
	</technique>

	<technique name="UpdateTypedUAVCS" inherit="UpdateCS">
		<macro name="USE_TYPED_UAV" value="1"/>
	</technique>
</effect>

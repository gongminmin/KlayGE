<?xml version='1.0'?>

<effect>
	<include name="Lighting.fxml"/>
	<include name="Quaternion.fxml"/>
	<include name="PostProcess.fxml"/>
	<include name="MotionBlur.fxml"/>
	<include name="Mesh.fxml"/>

	<cbuffer name="per_frame">
		<parameter type="float4x4" name="view"/>
		<parameter type="float4x4" name="proj"/>
		<parameter type="float3" name="light_in_world" x="2" y="2" z="-3"/>
		<parameter type="float3" name="eye_in_world"/>
	</cbuffer>

	<parameter type="float4x4" name="modelmat" semantic="WORLD"/>
	<parameter type="float4x4" name="last_modelmat"/>
	<parameter type="float4" name="color"/>

	<macro name="EPSILON" value="0.01f"/>

	<shader>
		<![CDATA[
void ColorDepthInstancedVS(float4 pos			: POSITION,
					float4 tangent_quat		: TANGENT,

					float4 row0			: TEXCOORD1,
					float4 row1			: TEXCOORD2,
					float4 row2			: TEXCOORD3,
					float4 last_row0	: TEXCOORD4,
					float4 last_row1	: TEXCOORD5,
					float4 last_row2	: TEXCOORD6,
					float3 clr			: COLOR0,

					out float3 oClr : TEXCOORD0,
					out float3 oPosWS : TEXCOORD1,
					out float3 oNormalWS : TEXCOORD2,
					out float3 oLightVecWS : TEXCOORD3,
					out float3 oHalfwayVecWS : TEXCOORD4,
					out float4 oPos		: SV_Position)
{
	pos = float4(pos.xyz * pos_extent + pos_center, 1);

	float4x4 model = { row0, row1, row2, float4(0, 0, 0, 1) };

	float4 pos_ws = mul(model, pos);
	float3 normal = mul((float3x3)model, transform_quat(float3(0, 0, 1), tangent_quat * 2 - 1));
	oPos = mul(mul(pos_ws, view), proj);
	oPosWS = pos_ws.xyz;
	oNormalWS = normal;
	oClr = clr;
	
	oLightVecWS = light_in_world - pos_ws.xyz;
	float3 view_vec = eye_in_world - pos_ws.xyz;
	oHalfwayVecWS = normalize(oLightVecWS) + normalize(view_vec);
}

void ColorDepthNonInstancedVS(float4 pos			: POSITION,
					float4 tangent_quat		: TANGENT,

					out float3 oClr : TEXCOORD0,
					out float3 oPosWS : TEXCOORD1,
					out float3 oNormalWS : TEXCOORD2,
					out float3 oLightVecWS : TEXCOORD3,
					out float3 oHalfwayVecWS : TEXCOORD4,
					out float4 oPos		: SV_Position)
{
	pos = float4(pos.xyz * pos_extent + pos_center, 1);
	tangent_quat = tangent_quat * 2 - 1;
	
	float4 pos_ws = mul(pos, modelmat);
	float3 normal = mul(transform_quat(float3(0, 0, 1), tangent_quat), (float3x3)modelmat);
	oPos = mul(mul(pos_ws, view), proj);
	oPosWS = pos_ws.xyz;
	oNormalWS = normal;
	oClr.xyz = color.rgb;
	
	oLightVecWS = light_in_world - pos_ws.xyz;
	float3 view_vec = eye_in_world - pos_ws.xyz;
	oHalfwayVecWS = normalize(oLightVecWS) + normalize(view_vec);
}

float4 ColorDepthPS(float3 clr : TEXCOORD0, float3 pos_ws : TEXCOORD1, float3 normal_ws : TEXCOORD2,
	float3 light_vec_ws : TEXCOORD3, float3 halfway_vec_ws : TEXCOORD4) : SV_Target
{
	light_vec_ws = normalize(light_vec_ws);
	halfway_vec_ws = normalize(halfway_vec_ws);
	normal_ws = normalize(normal_ws);

	float metalness = 0.7f;
	float3 c_diff = DiffuseColor(clr, metalness);
	float3 c_spec = SpecularColor(clr, metalness);
	
	return float4(0.05f * clr + CalcBRDFShading(c_diff, c_spec, 35, light_vec_ws, halfway_vec_ws, normal_ws), 1);
}
		]]>
	</shader>

	<technique name="ColorDepthInstanced">
		<pass name="p0">
			<state name="vertex_shader" value="ColorDepthInstancedVS()"/>
			<state name="pixel_shader" value="ColorDepthPS()"/>
		</pass>
	</technique>
	
	<technique name="ColorDepthNonInstanced">
		<pass name="p0">
			<state name="vertex_shader" value="ColorDepthNonInstancedVS()"/>
			<state name="pixel_shader" value="ColorDepthPS()"/>
		</pass>
	</technique>

	<cbuffer name="per_frame">
		<parameter type="float4x4" name="prev_view"/>
		<parameter type="float4x4" name="prev_proj"/>
		<parameter type="float" name="half_exposure_x_framerate"/>
	</cbuffer>
	
	<shader>
		<![CDATA[
void CalcMotionVector(float4 pos_curr, float4 pos_prev, float3 normal_es,
					out float4 oPosCurr, out float4 oPosPrev)
{
	pos_curr = mul(pos_curr, view);
	pos_prev = mul(pos_prev, prev_view);

	oPosCurr = mul(pos_curr, proj);
	oPosPrev = mul(pos_prev, prev_proj);
}

void VelocityInstancedVS(float4 pos			: POSITION,
					float4 tangent_quat		: TANGENT,

					float4 row0			: TEXCOORD1,
					float4 row1			: TEXCOORD2,
					float4 row2			: TEXCOORD3,
					float4 last_row0	: TEXCOORD4,
					float4 last_row1	: TEXCOORD5,
					float4 last_row2	: TEXCOORD6,
					float3 clr			: COLOR0,

					out float4 oPosCurr : TEXCOORD0,
					out float4 oPosPrev : TEXCOORD1,
					out float4 oPos		: SV_Position)
{
	pos = float4(pos.xyz * pos_extent + pos_center, 1);
	tangent_quat = tangent_quat * 2 - 1;
	
	float4x4 model = { row0, row1, row2, float4(0, 0, 0, 1) };
	float4x4 last_model = { last_row0, last_row1, last_row2, float4(0, 0, 0, 1) };

	float3 normal = mul(mul((float3x3)model, transform_quat(float3(0, 0, 1), tangent_quat)), (float3x3)view);
	CalcMotionVector(mul(model, pos), mul(last_model, pos), normal, oPosCurr, oPosPrev);

	oPos = oPosCurr;
}

void VelocityNonInstancedVS(float4 pos		: POSITION,
					float4 tangent_quat		: TANGENT,

					out float4 oPosCurr : TEXCOORD0,
					out float4 oPosPrev : TEXCOORD1,
					out float4 oPos		: SV_Position)
{
	pos = float4(pos.xyz * pos_extent + pos_center, 1);
	tangent_quat = tangent_quat * 2 - 1;
	
	float4 pos_ws = mul(pos, modelmat);
	float3 normal = mul(mul(transform_quat(float3(0, 0, 1), tangent_quat), (float3x3)modelmat), (float3x3)view);
	CalcMotionVector(mul(pos, modelmat), mul(pos, last_modelmat), normal, oPosCurr, oPosPrev);

	oPos = oPosCurr;
}

float4 VelocityPS(float4 pos_curr : TEXCOORD0, float4 pos_prev : TEXCOORD1) : SV_Target
{
	return float4(GenerateVelocity(pos_curr, pos_prev, half_exposure_x_framerate), 1, 1);
}
		]]>
	</shader>

	<technique name="VelocityInstanced">
		<pass name="p0">
			<state name="vertex_shader" value="VelocityInstancedVS()"/>
			<state name="pixel_shader" value="VelocityPS()"/>
		</pass>
	</technique>

	<technique name="VelocityNonInstanced">
		<pass name="p0">
			<state name="vertex_shader" value="VelocityNonInstancedVS()"/>
			<state name="pixel_shader" value="VelocityPS()"/>
		</pass>
	</technique>
</effect>

<?xml version='1.0'?>

<effect>
	<include name="PostProcess.fxml"/>

	<parameter type="float3" name="addr_offset"/>
	<parameter type="int" name="length"/>
	<parameter type="float" name="scale"/>

	<parameter type="texture2D" name="src_tex"/>
	<parameter type="texture2D" name="child_tex"/>

	<parameter type="sampler" name="tex_sampler">
		<state name="filtering" value="min_mag_mip_point"/>
		<state name="address_u" value="border"/>
		<state name="address_v" value="border"/>
		<state name="border_clr" r="0" g="0" b="0" a="0"/>
	</parameter>

	<shader>
		<![CDATA[
float4 SATScanXUpSweepPS(float2 tex_coord0 : TEXCOORD0) : SV_Target
{
	float tar_x = tex_coord0.x * scale;
	return src_tex.Sample(tex_sampler, float2(tar_x - addr_offset.y, tex_coord0.y))
				+ src_tex.Sample(tex_sampler, float2(tar_x - addr_offset.x, tex_coord0.y))
				+ src_tex.Sample(tex_sampler, float2(tar_x + addr_offset.x, tex_coord0.y))
				+ src_tex.Sample(tex_sampler, float2(tar_x + addr_offset.y, tex_coord0.y));
}

float4 SATScanXDownSweepPS(float2 tex_coord0 : TEXCOORD0) : SV_Target
{
	float4 clr;
	float x = floor(tex_coord0.x * length);
	int r = fmod(x, 4);
	if (3 == r)
	{
		clr = src_tex.Sample(tex_sampler, float2(tex_coord0.x * scale, tex_coord0.y));
	}
	else
	{
		clr = child_tex.Sample(tex_sampler, tex_coord0);
		if (x > 3)
		{
			clr += src_tex.Sample(tex_sampler, float2(tex_coord0.x * scale - addr_offset.x, tex_coord0.y));
		}
		if (r >= 1)
		{
			clr += child_tex.Sample(tex_sampler, tex_coord0 - float2(addr_offset.y, 0));
		}
		if (r >= 2)
		{
			clr += child_tex.Sample(tex_sampler, tex_coord0 - float2(addr_offset.z, 0));
		}
	}
	return clr;
}

float4 SATScanYUpSweepPS(float2 tex_coord0 : TEXCOORD0) : SV_Target
{
	float tar_y = tex_coord0.y * scale;
	return src_tex.Sample(tex_sampler, float2(tex_coord0.x, tar_y - addr_offset.y))
				+ src_tex.Sample(tex_sampler, float2(tex_coord0.x, tar_y - addr_offset.x))
				+ src_tex.Sample(tex_sampler, float2(tex_coord0.x, tar_y + addr_offset.x))
				+ src_tex.Sample(tex_sampler, float2(tex_coord0.x, tar_y + addr_offset.y));
}

float4 SATScanYDownSweepPS(float2 tex_coord0 : TEXCOORD0) : SV_Target
{
	float4 clr;
	float y = floor(tex_coord0.y * length);
	int r = fmod(y, 4);
	if (3 == r)
	{
		clr = src_tex.Sample(tex_sampler, float2(tex_coord0.x, tex_coord0.y * scale));
	}
	else
	{
		clr = child_tex.Sample(tex_sampler, tex_coord0);
		if (y > 3)
		{
			clr += src_tex.Sample(tex_sampler, float2(tex_coord0.x, tex_coord0.y * scale - addr_offset.x));
		}
		if (r >= 1)
		{
			clr += child_tex.Sample(tex_sampler, tex_coord0 - float2(0, addr_offset.y));
		}
		if (r >= 2)
		{
			clr += child_tex.Sample(tex_sampler, tex_coord0 - float2(0, addr_offset.z));
		}
	}
	return clr;
}
		]]>
	</shader>
	<technique name="SATScanXUpSweep">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="PostProcessVS()"/>
			<state name="pixel_shader" value="SATScanXUpSweepPS()"/>
		</pass>
	</technique>
	<technique name="SATScanXDownSweep">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="PostProcessVS()"/>
			<state name="pixel_shader" value="SATScanXDownSweepPS()"/>
		</pass>
	</technique>	

	<technique name="SATScanYUpSweep">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="PostProcessVS()"/>
			<state name="pixel_shader" value="SATScanYUpSweepPS()"/>
		</pass>
	</technique>

	<technique name="SATScanYDownSweep">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="PostProcessVS()"/>
			<state name="pixel_shader" value="SATScanYDownSweepPS()"/>
		</pass>
	</technique>

	<macro name="BLOCK_SIZE" value="128"/>
	<macro name="SCAN_TYPE" value="float4"/>
	<include name="PrefixSum.fxml"/>

	<parameter type="rw_texture2D" elem_type="float4" name="out_tex"/>
	<parameter type="texture2D" name="in_tex"/>
	<parameter type="int" name="num_waves"/>

	<shader type="compute_shader" version="5">
		<![CDATA[
[numthreads(BLOCK_SIZE, 1, 1)]
void InclusiveScanXCS(uint3 dtid : SV_DispatchThreadID, 
				uint GI : SV_GroupIndex)
{
	uint x = dtid.x;
	float4 sum = 0;
	for (int w = 0; w < num_waves; ++ w)
	{
		float4 val = ScanBlock(true, GI, in_tex[int2(x, dtid.y)]) + sum;
		out_tex[int2(x, dtid.y)] = val;

		if (BLOCK_SIZE - 1 == GI)
		{
			forscan_sh[BLOCK_SIZE - 1] = val;
		}
		GroupMemoryBarrierWithGroupSync();

		sum = forscan_sh[BLOCK_SIZE - 1];
		x += BLOCK_SIZE;
		GroupMemoryBarrierWithGroupSync();
	}
}

[numthreads(1, BLOCK_SIZE, 1)]
void InclusiveScanYCS(uint3 dtid : SV_DispatchThreadID, 
				uint GI : SV_GroupIndex)
{
	uint y = dtid.y;
	float4 sum = 0;
	for (int w = 0; w < num_waves; ++ w)
	{
		float4 val = ScanBlock(true, GI, in_tex[int2(dtid.x, y)]) + sum;
		out_tex[int2(dtid.x, y)] = val;

		if (BLOCK_SIZE - 1 == GI)
		{
			forscan_sh[BLOCK_SIZE - 1] = val;
		}
		GroupMemoryBarrierWithGroupSync();

		sum = forscan_sh[BLOCK_SIZE - 1];
		y += BLOCK_SIZE;
		GroupMemoryBarrierWithGroupSync();
	}
}
		]]>
	</shader>

	<technique name="SATScanXCS">
		<pass name="p0">
			<state name="compute_shader" value="InclusiveScanXCS()"/>
		</pass>
	</technique>
	<technique name="SATScanYCS">
		<pass name="p0">
			<state name="compute_shader" value="InclusiveScanYCS()"/>
		</pass>
	</technique>
</effect>

<?xml version='1.0'?>

<effect>
	<include name="util.fxml"/>
	<include name="Copy.fxml"/>
	<include name="Lighting.fxml"/>
	<include name="DeferredRenderingUtil.fxml"/>

	<cbuffer name="sss_cb">
		<parameter type="float2" name="step"/>
		<parameter type="float" name="far_plane"/>
		<parameter type="float" name="correction"/>
	</cbuffer>

	<parameter type="texture2D" name="depth_tex"/>
	<parameter type="texture2DMS" name="depth_tex_ms"/>

	<shader>
		<![CDATA[
float4 SeparableSssBlurPS(float2 texcoord, uint sample_index, float2 dir)
{
	const int SSSS_N_SAMPLES = 11;
	float4 kernel[] =
	{
		float4(0.560479, 0.669086, 0.784728, 0),
		float4(0.00471691, 0.000184771, 5.07566e-005, -2),
		float4(0.0192831, 0.00282018, 0.00084214, -1.28),
		float4(0.03639, 0.0130999, 0.00643685, -0.72),
		float4(0.0821904, 0.0358608, 0.0209261, -0.32),
		float4(0.0771802, 0.113491, 0.0793803, -0.08),
		float4(0.0771802, 0.113491, 0.0793803, 0.08),
		float4(0.0821904, 0.0358608, 0.0209261, 0.32),
		float4(0.03639, 0.0130999, 0.00643685, 0.72),
		float4(0.0192831, 0.00282018, 0.00084214, 1.28),
		float4(0.00471691, 0.000184771, 5.07565e-005, 2),
	};

	// Fetch color and linear depth for current pixel:
#if KLAYGE_EXPLICIT_MULTI_SAMPLE_SUPPORT && MULTI_SAMPLE_INPUT
	uint width, height, samples;
	src_tex_ms.GetDimensions(width, height, samples);
	int2 coord = texcoord * int2(width, height);

	float4 color_m = src_tex_ms.Load(coord, sample_index);
	float depth_m = ReadAFloat(depth_tex_ms.Load(coord, sample_index), far_plane);
#else
	float4 color_m = src_tex.Sample(point_sampler, texcoord);
	float depth_m = ReadAFloat(depth_tex.Sample(point_sampler, texcoord), far_plane);
#endif

	// Accumulate center sample, multiplying it with its gaussian weight:
	float4 color_blurred = color_m;
	color_blurred.rgb *= kernel[0].rgb;
	
	// Calculate the step that we will use to fetch the surrounding pixels,
	// where "step" is:
	//     step = sss_strength * gaussian_width * pixel_size * dir
	// The closer the pixel, the stronger the effect needs to be, hence
	// the factor 1.0 / depth_m.
	float2 final_step = color_m.a * step / depth_m;
	for (int i = 1; i < SSSS_N_SAMPLES; ++ i)
	{
		// Fetch color and depth for current sample:
		float2 offset = texcoord + kernel[i].a * final_step;
#if KLAYGE_EXPLICIT_MULTI_SAMPLE_SUPPORT && MULTI_SAMPLE_INPUT
		int2 offset_coord = offset * int2(width, height);
		float3 color = src_tex_ms.Load(offset_coord, sample_index).rgb;
		float depth = ReadAFloat(depth_tex_ms.Load(offset_coord, sample_index), far_plane);
#else
		float3 color = src_tex.Sample(point_sampler, offset).rgb;
		float depth = ReadAFloat(depth_tex.Sample(point_sampler, offset), far_plane);
#endif

		// If the difference in depth is huge, we lerp color back to "color_m":
		float s = min(0.0125f * correction * abs(depth_m - depth), 1);
		color = lerp(color, color_m.rgb, s);

		// Accumulate:
		color_blurred.rgb += kernel[i].rgb * color;
	}

	// The result will be alpha blended with current buffer by using specific
	// RGB weights. For more details, I refer you to the GPU Pro chapter :)
	return color_blurred;
}

float4 SeparableSssBlurXPS(float2 texcoord : TEXCOORD0, uint sample_index : SV_SampleIndex) : SV_Target
{
	return SeparableSssBlurPS(texcoord, sample_index, float2(1, 0));
}

float4 SeparableSssBlurYPS(float2 texcoord : TEXCOORD0, uint sample_index : SV_SampleIndex) : SV_Target
{
	return SeparableSssBlurPS(texcoord, sample_index, float2(0, 1));
}
		]]>
	</shader>

	<technique name="SeparableSssBlurX">
		<pass name="p1">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="false"/>

			<state name="front_stencil_enable" value="true"/>
			<state name="front_stencil_func" value="equal"/>
			<state name="front_stencil_ref" value="32"/>
			<state name="front_stencil_read_mask" value="32"/>
			<state name="front_stencil_write_mask" value="0"/>

			<state name="vertex_shader" value="PostProcessVS()"/>
			<state name="pixel_shader" value="SeparableSssBlurXPS()"/>
		</pass>
	</technique>
	<technique name="SeparableSssBlurXMS" inherit="SeparableSssBlurX">
		<macro name="MULTI_SAMPLE_INPUT" value="1"/>
	</technique>

	<technique name="SeparableSssBlurY" inherit="SeparableSssBlurX">
		<pass name="p1">
			<state name="pixel_shader" value="SeparableSssBlurYPS()"/>
		</pass>
	</technique>
	<technique name="SeparableSssBlurYMS" inherit="SeparableSssBlurY">
		<macro name="MULTI_SAMPLE_INPUT" value="1"/>
	</technique>


	<cbuffer name="translucency_cb">
		<parameter type="float4x4" name="inv_proj"/>
		<parameter type="float4x4" name="light_view_proj"/>
		<parameter type="float4x4" name="light_view"/>
		<parameter type="float3" name="light_color"/>
		<parameter type="float3" name="light_falloff"/>
		<parameter type="float3" name="light_pos_es"/>
		<parameter type="float" name="strength"/>
		<parameter type="float" name="sm_far_plane"/>
	</cbuffer>

	<parameter type="texture2D" name="shadow_tex"/>
	<parameter type="texture2D" name="g_buffer_rt0_tex"/>
	<parameter type="texture2D" name="g_buffer_rt1_tex"/>
	<parameter type="texture2DMS" name="g_buffer_rt0_tex_ms"/>
	<parameter type="texture2DMS" name="g_buffer_rt1_tex_ms"/>
	
	<shader>
		<![CDATA[
float3 CalcTransmit(float s)
{
	return float3(0.233f, 0.455f, 0.649f) * exp(-s * s / 0.0064f)
		+ float3(0.1f, 0.336f, 0.344f) * exp(-s * s / 0.0484f)
		+ float3(0.118f, 0.198f, 0.0f) * exp(-s * s / 0.187f)
		+ float3(0.113f, 0.007f, 0.007f) * exp(-s * s / 0.567f)
		+ float3(0.358f, 0.004f, 0.0f) * exp(-s * s / 1.99f)
		+ float3(0.078f, 0.0f, 0.0f) * exp(-s * s / 7.41f);
}

void TranslucencyVS(float4 pos : POSITION,
					out float2 oTex : TEXCOORD0,
					out float3 oViewDir : TEXCOORD1,
					out float4 oPos : SV_Position)
{
	oTex = TexCoordFromPos(pos);
	oPos = pos;

	oViewDir = mul(oPos, inv_proj).xyz;
}

float4 TranslucencyPS(float2 tc : TEXCOORD0, float3 view_dir : TEXCOORD1, uint sample_index : SV_SampleIndex) : SV_Target0
{
#if KLAYGE_EXPLICIT_MULTI_SAMPLE_SUPPORT && MULTI_SAMPLE_INPUT
	uint width, height, samples;
	g_buffer_rt0_tex_ms.GetDimensions(width, height, samples);
	int2 coord = tc * int2(width, height);

	float4 rt0 = g_buffer_rt0_tex_ms.Load(coord, sample_index);
	float4 rt1 = g_buffer_rt1_tex_ms.Load(coord, sample_index);
	float depth = ReadAFloat(depth_tex_ms.Load(coord, sample_index), far_plane);
#else
	float4 rt0 = g_buffer_rt0_tex.Sample(point_sampler, tc);
	float4 rt1 = g_buffer_rt1_tex.Sample(point_sampler, tc);
	float depth = ReadAFloat(depth_tex.Sample(point_sampler, tc), far_plane);
#endif

	float3 pos_es = view_dir * (depth / view_dir.z);
	float4 light_vp_pos = mul(float4(pos_es, 1), light_view_proj);
	float4 light_view_pos = mul(float4(pos_es, 1), light_view);
	float3 normal = GetNormal(rt0);

	float2 coord_ls = light_vp_pos.xy / light_vp_pos.w * 0.5f;
	coord_ls.y *= KLAYGE_FLIPPING;
	coord_ls += 0.5f;
	float s = abs(length(light_view_pos.xyz) - ReadAFloat(shadow_tex.Sample(bilinear_sampler, coord_ls), sm_far_plane));

	float3 color = light_color * AttenuationTerm(light_pos_es, pos_es, light_falloff);
	float3 light_vec = normalize(pos_es - light_pos_es);
	float irradiance = saturate(0.3f + dot(normal, light_vec));
	float3 transmittance = CalcTransmit(s * strength) * color * irradiance * GetDiffuse(rt1).rgb;
	return float4(transmittance, 1);
}
		]]>
	</shader>

	<technique name="Translucency">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="false"/>

			<state name="front_stencil_enable" value="true"/>
			<state name="front_stencil_func" value="equal"/>
			<state name="front_stencil_ref" value="32"/>
			<state name="front_stencil_read_mask" value="32"/>
			<state name="front_stencil_write_mask" value="0"/>

			<state name="blend_enable" value="true"/>
			<state name="blend_op" value="add"/>
			<state name="src_blend" value="one"/>
			<state name="dest_blend" value="one"/>
			<state name="blend_op_alpha" value="add"/>
			<state name="src_blend_alpha" value="one"/>
			<state name="dest_blend_alpha" value="one"/>

			<state name="vertex_shader" value="TranslucencyVS()"/>
			<state name="pixel_shader" value="TranslucencyPS()"/>
		</pass>
	</technique>
	<technique name="TranslucencyMS" inherit="Translucency">
		<macro name="MULTI_SAMPLE_INPUT" value="1"/>
	</technique>
</effect>

LIST(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/../../../cmake")

PROJECT(glloader)

CMAKE_MINIMUM_REQUIRED(VERSION 2.8.6)

IF(COMMAND cmake_policy)
	CMAKE_POLICY(SET CMP0003 NEW)
ENDIF(COMMAND cmake_policy)

SET(GLLOADER_MAJOR_VERSION 4)
SET(GLLOADER_MINOR_VERSION 0)
SET(GLLOADER_PATCH_VERSION 0)
SET(GLLOADER_VERSION ${GLLOADER_MAJOR_VERSION}.${GLLOADER_MINOR_VERSION}.${GLLOADER_PATCH_VERSION})

FIND_PACKAGE(PythonInterp)

SET(GLLOADER_PROJECT_DIR "${CMAKE_CURRENT_SOURCE_DIR}/../..")
SET(KLAYGE_ROOT_DIR "${GLLOADER_PROJECT_DIR}/..")

INCLUDE(${KLAYGE_ROOT_DIR}/cmake/Common.cmake)
INCLUDE(${KLAYGE_ROOT_DIR}/cmake/Platform.cmake)
INCLUDE(${KLAYGE_ROOT_DIR}/cmake/Compiler.cmake)

SET_PROPERTY(GLOBAL PROPERTY USE_FOLDERS ON)

IF(KLAYGE_PLATFORM_DARWIN OR KLAYGE_PLATFORM_IOS)
	FIND_LIBRARY(COREFOUNDATION CoreFoundation "/")
	SET(EXTRA_LIBRARIES ${COREFOUNDATION})
ENDIF()

SET(GLLOADER_OUTPUT_DIR "${CMAKE_HOME_DIRECTORY}/../../lib/${KLAYGE_PLATFORM_NAME}")
SET(KLAYGE_BIN_DIR "${CMAKE_HOME_DIRECTORY}/../../../KlayGE/bin/${KLAYGE_PLATFORM_NAME}")


ADD_CUSTOM_TARGET(autogen ALL "${PYTHON_EXECUTABLE}" "${GLLOADER_PROJECT_DIR}/autogen.py" "-q")

# The project settings

MACRO(SETUP_GLLOADER LIB_NAME)
	SET(GLLOADER_SOURCE_FILES
		${GLLOADER_PROJECT_DIR}/src/glloader_egl.c
		${GLLOADER_PROJECT_DIR}/src/glloader_gl.c
		${GLLOADER_PROJECT_DIR}/src/glloader_gles.c
		${GLLOADER_PROJECT_DIR}/src/glloader_glx.c
		${GLLOADER_PROJECT_DIR}/src/glloader_wgl.c
		${GLLOADER_PROJECT_DIR}/src/utils.cpp
	)
	SET(GLLOADER_INCLUDE_FILES
		${GLLOADER_PROJECT_DIR}/include/glloader/glloader.h
		${GLLOADER_PROJECT_DIR}/include/glloader/glloader_egl.h
		${GLLOADER_PROJECT_DIR}/include/glloader/glloader_gl.h
		${GLLOADER_PROJECT_DIR}/include/glloader/glloader_gles.h
		${GLLOADER_PROJECT_DIR}/include/glloader/glloader_glx.h
		${GLLOADER_PROJECT_DIR}/include/glloader/glloader_wgl.h
	)
	SET(GLLOADER_HEADER_FILES
		${GLLOADER_PROJECT_DIR}/src/utils.h
	)
	SOURCE_GROUP("Source Files" FILES ${GLLOADER_SOURCE_FILES})
	SOURCE_GROUP("Header Files" FILES ${GLLOADER_HEADER_FILES})
	SOURCE_GROUP("Include Files" FILES ${GLLOADER_INCLUDE_FILES})

	SET_SOURCE_FILES_PROPERTIES(${GLLOADER_PROJECT_DIR}/src/glloader_egl.c PROPERTIES GENERATED 1)
	SET_SOURCE_FILES_PROPERTIES(${GLLOADER_PROJECT_DIR}/src/glloader_gl.c PROPERTIES GENERATED 1)
	SET_SOURCE_FILES_PROPERTIES(${GLLOADER_PROJECT_DIR}/src/glloader_gles.c PROPERTIES GENERATED 1)
	SET_SOURCE_FILES_PROPERTIES(${GLLOADER_PROJECT_DIR}/src/glloader_glx.c PROPERTIES GENERATED 1)
	SET_SOURCE_FILES_PROPERTIES(${GLLOADER_PROJECT_DIR}/src/glloader_wgl.c PROPERTIES GENERATED 1)
	SET_SOURCE_FILES_PROPERTIES(${GLLOADER_PROJECT_DIR}/include/glloader/glloader_egl.h PROPERTIES GENERATED 1)
	SET_SOURCE_FILES_PROPERTIES(${GLLOADER_PROJECT_DIR}/include/glloader/glloader_gl.h PROPERTIES GENERATED 1)
	SET_SOURCE_FILES_PROPERTIES(${GLLOADER_PROJECT_DIR}/include/glloader/glloader_gles.h PROPERTIES GENERATED 1)
	SET_SOURCE_FILES_PROPERTIES(${GLLOADER_PROJECT_DIR}/include/glloader/glloader_glx.h PROPERTIES GENERATED 1)
	SET_SOURCE_FILES_PROPERTIES(${GLLOADER_PROJECT_DIR}/include/glloader/glloader_wgl.h PROPERTIES GENERATED 1)

	ADD_DEFINITIONS(-DGLLOADER_SOURCE)
	INCLUDE_DIRECTORIES(${GLLOADER_PROJECT_DIR}/include)
	ADD_LIBRARY(${LIB_NAME} ${KLAYGE_PREFERRED_LIB_TYPE}
		${GLLOADER_SOURCE_FILES} ${GLLOADER_HEADER_FILES} ${GLLOADER_INCLUDE_FILES}
	)

	SET(LIB_OUTPUT_NAME ${LIB_NAME}${KLAYGE_OUTPUT_SUFFIX})
	SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES
		ARCHIVE_OUTPUT_DIRECTORY ${GLLOADER_OUTPUT_DIR}
		ARCHIVE_OUTPUT_DIRECTORY_DEBUG ${GLLOADER_OUTPUT_DIR}
		ARCHIVE_OUTPUT_DIRECTORY_RELEASE ${GLLOADER_OUTPUT_DIR}
		ARCHIVE_OUTPUT_DIRECTORY_RELWITHDEBINFO ${GLLOADER_OUTPUT_DIR}
		ARCHIVE_OUTPUT_DIRECTORY_MINSIZEREL ${GLLOADER_OUTPUT_DIR}
		PROJECT_LABEL ${LIB_NAME}
		DEBUG_POSTFIX ${CMAKE_DEBUG_POSTFIX}
		OUTPUT_NAME ${LIB_OUTPUT_NAME}
	)
	IF(KLAYGE_PLATFORM_DARWIN OR KLAYGE_PLATFORM_LINUX)
		SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES
			LIBRARY_OUTPUT_DIRECTORY ${KLAYGE_BIN_DIR}
			LIBRARY_OUTPUT_DIRECTORY_DEBUG ${KLAYGE_BIN_DIR}
			LIBRARY_OUTPUT_DIRECTORY_RELEASE ${KLAYGE_BIN_DIR}
			LIBRARY_OUTPUT_DIRECTORY_RELWITHDEBINFO ${KLAYGE_BIN_DIR}
			LIBRARY_OUTPUT_DIRECTORY_MINSIZEREL ${KLAYGE_BIN_DIR})
	ENDIF()

	ADD_DEPENDENCIES(${LIB_NAME} autogen)

	TARGET_LINK_LIBRARIES(${LIB_NAME} ${EXTRA_LIBRARIES})

	IF(KLAYGE_PLATFORM_WINDOWS)
		ADD_POST_BUILD(${LIB_NAME} "")

		INSTALL(TARGETS ${LIB_NAME}
			RUNTIME DESTINATION ${KLAYGE_BIN_DIR}
			LIBRARY DESTINATION ${KLAYGE_BIN_DIR}
			ARCHIVE DESTINATION ${GLLOADER_OUTPUT_DIR}
		)
	ENDIF()
ENDMACRO(SETUP_GLLOADER)

ADD_SUBDIRECTORY(glloader)
ADD_SUBDIRECTORY(glloader_es)
